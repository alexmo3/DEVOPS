* >Natural Source Header 000000
* :Mode S
* :CP
* :LineIncrement 10
* <Natural Source Header
**************************************************************
* SISTEMA    : Sistema de Identificacao Criminal             *
* PROGRAMA   : SCRP1201                                      *
* O QUE FAZ  ? ALTERACAO DA FICHA INDICE CRIMINAL REDUZIDA   *
* AUTOR      : MARIA GORETE M S CRUZ                         *
* DATA       : JULHO DE 1999                                 *
* ALTERACAO : CONSULTA A HOM. TELA REDUZIDA - GORETE NOV/99  *
*             MIGRAÇÃO - MAR/2011 - CZ                       *
*             INCLUSÃO DE UMA NOVA LEITURA NO CRIMINAL PARA  *
*             POSSIBILITAR A GRAVACAO SEM CANCELAMENTO POR   *
*             3144.     07042011   CZ                        *
**************************************************************
DEFINE DATA
GLOBAL USING SCRG001
LOCAL  USING SICSENHA
LOCAL  USING SCRLNUME
LOCAL
1  CRIMINAIS VIEW OF SIC-CADASTRO
*  2 CODIGO-FONETICO-PRINCIPAL
*  2 CODIGO-FONETICO-SECUNDARIO     (1:61)
*  2 CODIGO-FONETICO-PRINCIPAL-MAE
*  2 CODIGO-FONETICO-SECUNDARIO-MAE (1:61)
   2 COD-FONETICO-PRINCIPAL
   2 COD-FONETICO-SECUNDARIO        (1:10)
   2 COD-FONETICO-PRINCIPAL-MAE
   2 COD-FONETICO-SECUNDARIO-MAE    (1:10)
   2 STATUS-REG
   2 REGISTRO-ESTADUAL-GERAL
   2 DATA-NASC
   2 NOME-COMPLETO
   2 NOME-MAE
*****************************************++
1 CRIMINAL-VIEW VIEW OF SIC-CADASTRO
  2 STATUS-REG
  2 LEGISLACAO
  2 TIPO-REG
  2 REGISTRO-ESTADUAL-GERAL
  2 NOME-COMPLETO
  2 NOME-PAI
  2 NOME-MAE
  2 DATA-REG
  2 APELIDO-ALCUNHAS
  2 DATA-NASC
  2 UF-REG-ESTADUAL-GERAL
  2 MUNICIPIO-REG-GERAL
  2 COD-MUNIC-REG-GERAL
  2 DATA-ALTERACAO-STATUS
  2 NOME-DELEGACIA
  2 SEXO
  2 NUMERO-AUTOS
  2 DATA-ATUACAO
  2 MATFUNC-ATU
  2 DATDOC-ATU
  2 HORA-ATU
  2 NOME-VITIMA(1:80)
  2 UF-CI
  2 OBS(1:80)
* 2 CODIGO-FONETICO-PRINCIPAL
* 2 CODIGO-FONETICO-SECUNDARIO(1:61)
* 2 CODIGO-FONETICO-PRINCIPAL-MAE
* 2 CODIGO-FONETICO-SECUNDARIO-MAE(1:61)
  2 COD-FONETICO-PRINCIPAL
  2 COD-FONETICO-SECUNDARIO        (1:10)
  2 COD-FONETICO-PRINCIPAL-MAE
  2 COD-FONETICO-SECUNDARIO-MAE    (1:10)
**
****************************
1 CRIMINAL-ATU VIEW OF SIC-CADASTRO
  2 STATUS-REG
  2 LEGISLACAO
  2 TIPO-REG
  2 REGISTRO-ESTADUAL-GERAL
  2 NOME-COMPLETO
  2 NOME-PAI
  2 NOME-MAE
  2 DATA-REG
  2 APELIDO-ALCUNHAS
  2 DATA-NASC
  2 UF-REG-ESTADUAL-GERAL
  2 MUNICIPIO-REG-GERAL
  2 COD-MUNIC-REG-GERAL
  2 DATA-ALTERACAO-STATUS
  2 NOME-DELEGACIA
  2 SEXO
  2 NUMERO-AUTOS
  2 DATA-ATUACAO
  2 MATFUNC-ATU
  2 DATDOC-ATU
  2 HORA-ATU
  2 NOME-VITIMA(1:80)
  2 UF-CI
  2 OBS(1:80)
* 2 CODIGO-FONETICO-PRINCIPAL
* 2 CODIGO-FONETICO-SECUNDARIO(1:61)
* 2 CODIGO-FONETICO-PRINCIPAL-MAE
* 2 CODIGO-FONETICO-SECUNDARIO-MAE(1:61)
  2 COD-FONETICO-PRINCIPAL
  2 COD-FONETICO-SECUNDARIO        (1:10)
  2 COD-FONETICO-PRINCIPAL-MAE
  2 COD-FONETICO-SECUNDARIO-MAE    (1:10)
**
****************************
1 #VARIAVEIS_DA_TELA
  2 #FUNC (A10)
  2 #CHV (A1)
  2 #CHAVE (A1)
  2 #ALARM (N2,0)
  2 #STATUS (A1)
  2 REDEFINE #STATUS
    3 #STATUS0(N1)
  2 #DESCREG (A45)
  2 #DESC-MUNICIPIO (A30)
  2 #MUNICIPIO (N4,0)
  2 #UF-REG-GERAL (A2)
  2 #REG_GERAL (N11,0)
  2 #DATA-REGISTRO (N8,0)
  2 REDEFINE #DATA-REGISTRO
    3 #D-REG(N2)
    3 #M-REG(N2)
    3 #A-REG(N4)
  2 #DATA-REGISTRO0 (N8)
  2 REDEFINE #DATA-REGISTRO0
    3 #A-REG0(N4)
    3 #M-REG0(N2)
    3 #D-REG0(N2)
  2 #DATA-ATUACAO (N8)
  2 REDEFINE #DATA-ATUACAO
    3 #ANO-AUTUACAO(N4)
    3 #MES-AUTUACAO(N2)
    3 #DIA-AUTUACAO(N2)
  2 #SUPER-ID     (A13)          /* era a(11)
  2 REDEFINE #SUPER-ID
    3 #IDENTIDADE (N11,0)
    3 #UF-CI      (A2)
  2 #NOME-COMPLETO (A60)
  2 #PAI (A60)
  2 #MAE (A60)
  2 #ALCUNHA (A30)
  2 #DATA_NASC (A8)
  2 REDEFINE #DATA_NASC
    3 #DIA_NASC(N2,0)
    3 #MES_NASC(N2,0)
    3 #ANO_NASC(N4,0)
  2 #DATA_NASC0 (A8)
  2 REDEFINE #DATA_NASC0
    3 #DATA_NASC00(N8,0)
    3 REDEFINE #DATA_NASC00
      4 #ANO_NASC0(N4,0)
      4 #MES_NASC0(N2,0)
      4 #DIA_NASC0(N2,0)
  2 #NATURALIDADE (A35)
  2 #COD-MUN-NASCIMENTO (N4,0)
  2 #UF-NASCIMENTO (A2)
  2 #DOC-APRESENTADO (A30)
  2 #NUM-DOC-APRESENTADO (N11)
  2 #ORGAO-EXP-UF (A15)
  2 #OCORRENCIA (N3,0)INIT<1>
  2 #OBSERVACOES (A77/1:4)
  2 REDEFINE #OBSERVACOES
    3 LEI-ART
      4 #OBSERVACOES-LEI(A30)
      4 #OBSERVACOES-ART(A30)
      4 #OBSERVACOES-FIL(A17)
    3 #OBSERVACOES-FI1(A77)
    3 #OBSERVACOES-FI2(A77)
    3 #OBSERVACOES-FI3(A77)
  2 #OBSERVACOEZ (A77/1:80)
  2 #NOME-VITIMA (A60/1:4)
  2 REDEFINE #NOME-VITIMA
    3 VIT1
      4 #VITIMA1(A59)
      4 #SEXO-VITIMA1(A1)
    3 VIT2
      4 #VITIMA2(A59)
      4 #SEXO-VITIMA2(A1)
    3 VIT3
      4 #VITIMA3(A59)
      4 #SEXO-VITIMA3(A1)
    3 VIT4
      4 #VITIMA4(A59)
      4 #SEXO-VITIMA4(A1)
  2 #VITIMAZ (A60/1:80)
  2 #CONF (A1)
  2 #DATA_ATU (N8,0)
  2 #HORA_ATU (N8,0)
  2 REDEFINE #HORA_ATU
    3 #HORA(N2,0)
    3 #MIN(N2,0)
    3 #SEG(N2,0)
    3 #NSE(N2,0)
  2 #OPERADOR (N7,0)
*********************
1 #I                (N04)
1 #J                (N04)
1 #L                (N04)
1 #M                (N04)
1 #X                (N04)
1 #Y                (N04)
1 #W                (N04)
1 #MAT              (N07)
1 #SENHA            (A06)
1 #LOOP             (N18)
1 #CV-REG-GER       (C)
1 #CV-DATA          (C)
1 #CV-IDENT         (C)
1 #CV-NOME          (C)
1 #CV-MAE          (C)
1 #VAR             (C)
1 #TEMPO(A10)
1 REDEFINE #TEMPO
2 #TEMPOHORA(N2,0)
2 FILLER 1X
2 #TEMPOMIN(N2,0)
2 FILLER 1X
2 #TEMPOSEG(N2,0)
2 FILLER 1X
2 #TEMPONSE(N1,0)
1 OPCAO             (N02)
1 EXIBIDO           (L)
1 ISN               (P08/1:14)
1 REGISTROS         (A11/1:14)
1 NOMES             (A60/1:14)
1 MAES              (A60/1:14)
1 DATAS             (A10/1:14)
1 IND               (N02)
1 #LISTA            (A01)
1 ACHOU             (A01)
1 MATESP                (N07)
1 SENESP                (A06)
1 #NIVEL-AUT            (A01)
1 #EMPRESA              (A08) INIT <' CODATA '>
1 #PROGRAM              (A08) INIT <*PROGRAM>
1 #FUNCAO               (A10) INIT <'ALTERACAO'>
1 CHAVE                 (A01) INIT <'A'>
1 DMANAS                (A08)
1 REDEFINE DMANAS
  2 DNAS                (N02)
  2 MNAS                (N02)
  2 ANAS                (N04)
1 DDDD                  (N08)
1 #DATA-ALT-STATUS      (N08)
1 REDEFINE #DATA-ALT-STATUS
  2 #ANO-ALT-STATUS     (N04)
  2 #MES-ALT-STATUS     (N02)
  2 #DIA-ALT-STATUS     (N02)
1 #ST-OLD               (N01)
1 #ULT-NUM              (N11)
1 #MAE-LIMPA            (A60)
1 #INFRACAO             (C)
1 #VAR-ST               (C)
1 #IMP                  (A08)
1 #MSGCRIM              (A60)
**** VARIAVEIS DA ROT-FONETICA
1 #EXISTE-SEC           (A01)
1 #NUMERO2              (N05)
1 #SEMELHANTE           (A01)
1 #J2                   (N02)
1 #K                    (N02)
1 #SEC                  (A06)
1 #SEC-ANT              (A06)
1 #AUX                  (A06)
1 #CHAVE-COMBINA        (A120)
1 REDEFINE #CHAVE-COMBINA
  2 #VET                      (A06/1:20)
1 #FONETO1                    (A06/1:61)
1 #FONETO-NOME-DADO           (A06)
1 REDEFINE #FONETO-NOME-DADO
  2 FONETO-NOME-DADO          (B06)
1 #FONETO-MAE-DADO            (A06)
1 REDEFINE #FONETO-MAE-DADO
  2 FONETO-MAE-DADO           (B06)
1 #INFORMACOES-GERAIS         (A97)
1 REDEFINE #INFORMACOES-GERAIS
  2 #NOME-DA-PESSOA           (A80)
  2 #TAMANHO-NOME-PESSOA      (B02)
  2 #TIPO-NOME                (A01)
  2 #NUMERO-COMPONENTES       (B02)
  2 #TIPO-COMBINACAO          (A01)
  2 #NUMERO-COMBINACOES       (B02)
  2 #INICIO-COMBINACAO        (B02)
  2 #CHAVE-NOME-DADO          (A06)
  2 REDEFINE #CHAVE-NOME-DADO
    3 CHAVE-NOME-DADO         (B06)
  2 #FLAG                     (A01)
**1 #FILLER                  (A100)
1 #CHAVE-COMB-2A21            (A240)
1 #CHAVE-COMB-2A22            (A030)
1 #CHAVE-COMB-3A31            (A240)
1 #CHAVE-COMB-3A32            (A240)
1 #CHAVE-COMB-3A33            (A240)
1 #CHAVE-COMB-4A41            (A240)
1 #CHAVE-COMB-4A42            (A240)
1 #CHAVE-COMB-4A43            (A240)
1 #CHAVE-COMB-4A44            (A240)
1 #CHAVE-COMB-4A45            (A240)
1 #CHAVE-COMB-4A46            (A240)
**
1 #CHAVE-FONETICA
  2 #CONSULTA                      (L)
  2 #TEXTO-CONSULTA                (A60)
  2 #TEXTO-FONETICO                (A15/1:10)
1 #CHAVE-FONETICA-MAE
  2 #CONSULTA-MAE                  (L)
  2 #TEXTO-CONSULTA-MAE            (A60)
  2 #TEXTO-FONETICO-MAE            (A15/1:10)
**
1 #FLAG-CRIMINAL                   (N01)
1 #reg-criminal                    (N11)  /* veronica 31.05.17 
1 #nao-pode-imprimir               (A01)  /* veronica 31.05.17 
*
1 #FLAGN(N1) /* HOMONIMO INDIVIDUO
1 #FLAGM(N1) /* HOMONIMO MAE
**
END-DEFINE
*
RESET #CONTADOR# DNAS MNAS ANAS #VARIAVEIS_DA_TELA #ST-OLD
FORMAT PS=23
SET KEY ALL
#PROGRAMA# := *PROGRAM
MATESP := #MATRICULA#
#FUNC := 'ALTERACAO'
SET CONTROL 'MB'
MOVE (AD=I) TO #VAR #CV-MAE #CV-NOME #VAR-ST
IF NOT (*USER = '18GO' OR = '18go')
MOVE (AD=IP) TO #VAR-ST
END-IF
REPEAT
  #VLT# := 2
  #CHV  := '*'
  ADD 1 TO #LOOP
  #OCORRENCIA := 1
  INPUT USING MAP 'SCRM2000'
  FD33. FIND (1) CRIMINAL-VIEW WITH
    REGISTRO-ESTADUAL-GERAL = #REG_GERAL
    IF *COUNTER(FD33.) GT 0
      IF STATUS-REG = 5
       REINPUT 'Esta ficha esta" excluida - Nao pode ser alterada' ALARM
      END-IF
      MOVE CRIMINAL-VIEW.DATA-NASC TO #DATA_NASC00
      #DIA_NASC := DNAS    := #DIA_NASC0
      #MES_NASC := MNAS    := #MES_NASC0
      #ANO_NASC := ANAS    := #ANO_NASC0
      MOVE CRIMINAL-VIEW.DATA-REG TO #DATA-REGISTRO0
      #A-REG := #A-REG0
      #M-REG := #M-REG0
      #D-REG := #D-REG0
      MOVE  CRIMINAL-VIEW.REGISTRO-ESTADUAL-GERAL  TO #REG_GERAL
      MOVE  CRIMINAL-VIEW.UF-REG-ESTADUAL-GERAL    TO #UF-CI
      MOVE  CRIMINAL-VIEW.STATUS-REG               TO #STATUS0 #ST-OLD
      MOVE  CRIMINAL-VIEW.DATA-ALTERACAO-STATUS    TO #DATA-ALT-STATUS
      MOVE  CRIMINAL-VIEW.NOME-COMPLETO            TO #NOME-COMPLETO
      MOVE  CRIMINAL-VIEW.NOME-PAI                 TO #PAI
      MOVE  CRIMINAL-VIEW.NOME-MAE                 TO #MAE
      MOVE  CRIMINAL-VIEW.MUNICIPIO-REG-GERAL      TO #DESC-MUNICIPIO
      MOVE  CRIMINAL-VIEW.COD-MUNIC-REG-GERAL      TO #MUNICIPIO
      MOVE  CRIMINAL-VIEW.UF-REG-ESTADUAL-GERAL    TO #UF-REG-GERAL
      MOVE  CRIMINAL-VIEW.DATA-ATUACAO             TO #DATA-ATUACAO
      MOVE  CRIMINAL-VIEW.APELIDO-ALCUNHAS         TO #ALCUNHA
*     MOVE  CRIMINAL-VIEW.NATURALIDADE             TO #NATURALIDADE
*    MOVE  CRIMINAL-VIEW.COD-MUNICIPIO-NASCIMENTO TO #COD-MUN-NASCIMENTO
*     MOVE  CRIMINAL-VIEW.UF-NATURALIDADE          TO #UF-NASCIMENTO
      MOVE  CRIMINAL-VIEW.NOME-VITIMA(*)           TO #VITIMAZ(*)
      MOVE  CRIMINAL-VIEW.OBS(*)                   TO #OBSERVACOEZ(*)
      #OBSERVACOES(1) := #OBSERVACOEZ(1)
      #OBSERVACOES(2) := #OBSERVACOEZ(2)
      #OBSERVACOES(3) := #OBSERVACOEZ(3)
      #OBSERVACOES(4) := #OBSERVACOEZ(4)
      #NOME-VITIMA(1) := #VITIMAZ(1)
      #NOME-VITIMA(2) := #VITIMAZ(2)
      #NOME-VITIMA(3) := #VITIMAZ(3)
      #NOME-VITIMA(4) := #VITIMAZ(4)
      PERFORM VER-STATUS
*
      MOVE (AD=IP) TO #CV-REG-GER
      INPUT USING MAP 'SCRM1201'
      IF *PF-KEY = 'PF3'
        FETCH 'SCRP1201'
      END-IF
*
      IF #ST-OLD NE #STATUS0
        SET CONTROL 'N'
        INPUT NO ERASE 21/25 'CONFIRMA ALTERACAO DE STATUS?' (AD=I)
          #CONF (AD=AIT'_')
        SET KEY PF3 ENTR
        IF NOT (*PF-KEY = 'PF3' OR = 'ENTR')
          REINPUT 'Tecla invalida' ALARM
        END-IF
        IF *PF-KEY = 'PF3'
          FETCH 'SCRP1201'
        END-IF
        IF #CONF = ' '
          REINPUT 'Confirme "S" ou "N"' MARK *#CONF ALARM
        END-IF
        IF NOT #CONF = 'S' OR = 'N'
          REINPUT 'Confirmacao invalida' MARK *#CONF ALARM
        END-IF
        IF #CONF = 'N'
          MOVE #ST-OLD TO #STATUS0
          INPUT MAP 'SCRM1201' NO ERASE
        ELSE
          PERFORM VER-STATUS
          MOVE *DATN TO #DATA-ALT-STATUS
*         INPUT MAP 'SCRM1201' NO ERASE
 INPUT WITH TEXT 'TECLE ENTER PARA CONTINUAR' MAP 'SCRM1201' NO ERASE
        END-IF
      END-IF
****
      MOVE DMANAS TO #DATA_NASC
      COMPUTE DDDD = (ANAS*10000) + (MNAS*100) + DNAS
      PERFORM CRIMINAL
*
      MOVE (AD=IP) TO #CV-REG-GER #INFRACAO #CV-IDENT #CV-NOME
        #CV-MAE #CV-DATA
      MOVE *DATN         TO #DATA_ATU
      MOVE *TIME         TO #TEMPO
      MOVE #TEMPOHORA    TO #HORA
      MOVE #TEMPOMIN     TO #MIN
      MOVE #TEMPOSEG     TO #SEG
      MOVE #TEMPONSE     TO #NSE
      MOVE #DATA_NASC    TO #DATA_NASC0
      MOVE #DIA_NASC     TO #DIA_NASC0
      MOVE #MES_NASC     TO #MES_NASC0
      MOVE #ANO_NASC     TO #ANO_NASC0
      MOVE #D-REG        TO #D-REG0
      MOVE #M-REG        TO #M-REG0
      MOVE #A-REG        TO #A-REG0
      REPEAT WHILE #CHV = '*'
        IF #OBSERVACOES(1) = ' '
          REINPUT 'NENHUMA OCORRENCIA FOI INFORMADA.'
            MARK *#OBSERVACOES(1) ALARM
        ELSE
          RESET #CHV
        END-IF
      END-REPEAT
      PERFORM GRAVA-OCORRENCIAS
*
      IF #MAE = '*' OR = ' '
        MOVE '************' TO #MAE
      END-IF
      IF #PAI = '*' OR = ' '
        MOVE '************' TO #PAI
      END-IF
      IF #ALCUNHA     = ' ' OR = '*'
        MOVE '************' TO #ALCUNHA
      END-IF
      IF #DATA-ATUACAO LT 1
        MOVE  0 TO #DATA-ATUACAO
      END-IF
*
   FIND CRIMINAL-ATU
       WITH CRIMINAL-ATU.REGISTRO-ESTADUAL-GERAL =
       CRIMINAL-VIEW.REGISTRO-ESTADUAL-GERAL
      CRIMINAL-atu.OBS(*)                   := #OBSERVACOEZ(*)
      CRIMINAL-atu.LEGISLACAO               := #OBSERVACOEZ(1)
      CRIMINAL-atu.NOME-VITIMA(*)           := #VITIMAZ(*)
      CRIMINAL-atu.REGISTRO-ESTADUAL-GERAL  := #REG_GERAL
      CRIMINAL-atu.STATUS-REG               := #STATUS0
      CRIMINAL-atu.DATA-ALTERACAO-STATUS    := #DATA-ALT-STATUS
      CRIMINAL-atu.TIPO-REG                 := 'AL'
      CRIMINAL-atu.NOME-COMPLETO            := #NOME-COMPLETO
      CRIMINAL-atu.NOME-PAI                 := #PAI
      CRIMINAL-atu.NOME-MAE                 := #MAE
      CRIMINAL-atu.DATA-NASC                := #DATA_NASC00
      CRIMINAL-atu.MUNICIPIO-REG-GERAL      := #DESC-MUNICIPIO
      CRIMINAL-atu.COD-MUNIC-REG-GERAL      := #MUNICIPIO
      CRIMINAL-atu.UF-REG-ESTADUAL-GERAL    := #UF-REG-GERAL
      CRIMINAL-atu.DATA-REG                 := #DATA-REGISTRO0
      CRIMINAL-atu.DATA-ATUACAO             := #DATA-ATUACAO
      CRIMINAL-atu.APELIDO-ALCUNHAS         := #ALCUNHA
      CRIMINAL-atu.DATDOC-ATU               := #DATA_ATU
      CRIMINAL-atu.HORA-ATU                 := #HORA_ATU
      CRIMINAL-atu.MATFUNC-ATU              := MATESP
**
      RESET #CHAVE-FONETICA
      #CONSULTA       := FALSE
      #TEXTO-CONSULTA := #NOME-COMPLETO
      CALLNAT 'NATBNX22' #CHAVE-FONETICA
      CRIMINAL-atu.COD-FONETICO-PRINCIPAL     := #TEXTO-FONETICO(1)
      CRIMINAL-atu.COD-FONETICO-SECUNDARIO(*) := #TEXTO-FONETICO(*)
**
      RESET #CHAVE-FONETICA-MAE
      #CONSULTA-MAE       := FALSE
      #TEXTO-CONSULTA-MAE := #MAE
      CALLNAT 'NATBNX22' #CHAVE-FONETICA-MAE
      CRIMINAL-atu.COD-FONETICO-PRINCIPAL-MAE := #TEXTO-FONETICO-MAE(1)
   CRIMINAL-atu.COD-FONETICO-SECUNDARIO-MAE(*):= #TEXTO-FONETICO-MAE(*)
**
*************************************************
**    UPDATE(FD33.)
      UPDATE(0455)
    END-FIND
    END-IF
  END-FIND
  IF *COUNTER(FD33.) EQ 0
    BACKOUT TRANSACTION
    REINPUT 'Ficha Criminal nao cadastrada' ALARM
  END-IF
**
  DEFINE SUBROUTINE GRAVA-OCORRENCIAS
  #I := 5
  IF #OBSERVACOES(1) NE ' '
    IF NOT (*PF-KEY = 'PF3' OR = 'ENTR')
      REINPUT 'Tecla invalida' ALARM
    END-IF
    #OBSERVACOEZ(1) := #OBSERVACOES(1) /* PEGA ALTERACOES DAS 4  1AS.
    #OBSERVACOEZ(2) := #OBSERVACOES(2) /* OCORRENCIAS JA MOSTRADAS
    #OBSERVACOEZ(3) := #OBSERVACOES(3)
    #OBSERVACOEZ(4) := #OBSERVACOES(4)
    #VITIMAZ(1) := #NOME-VITIMA(1)
    #VITIMAZ(2) := #NOME-VITIMA(2)
    #VITIMAZ(3) := #NOME-VITIMA(3)
    #VITIMAZ(4) := #NOME-VITIMA(4)
  END-IF
  FOR #OCORRENCIA = 2 TO 20
    IF #OCORRENCIA = 2
      COMPUTE #J = #OCORRENCIA + 4
      COMPUTE #L = #OCORRENCIA + 5
      COMPUTE #M = #OCORRENCIA + 6
    ELSE
      COMPUTE #I = #M + 1
      COMPUTE #J = #M + 2
      COMPUTE #L = #M + 3
      COMPUTE #M = #M + 4
    END-IF
    #OBSERVACOES(1)  := #OBSERVACOEZ(#I)
    #OBSERVACOES(2)  := #OBSERVACOEZ(#J)
    #OBSERVACOES(3)  := #OBSERVACOEZ(#L)
    #OBSERVACOES(4)  := #OBSERVACOEZ(#M)
    #NOME-VITIMA(1)  := #VITIMAZ(#I)
    #NOME-VITIMA(2)  := #VITIMAZ(#J)
    #NOME-VITIMA(3)  := #VITIMAZ(#L)
    #NOME-VITIMA(4)  := #VITIMAZ(#M)
    INPUT MAP 'SCRM1201'
    SET KEY PF3 ENTR
    IF NOT (*PF-KEY = 'PF3' OR = 'ENTR')
      REINPUT 'Tecla invalida' ALARM
    END-IF
    IF *PF-KEY = 'PF3'
      FETCH 'SCRP1201'
    END-IF
    #OBSERVACOEZ(#I) := #OBSERVACOES(1)
    #OBSERVACOEZ(#J) := #OBSERVACOES(2)
    #OBSERVACOEZ(#L) := #OBSERVACOES(3)
    #OBSERVACOEZ(#M) := #OBSERVACOES(4)
    #VITIMAZ(#I) := #NOME-VITIMA(1)
    #VITIMAZ(#J) := #NOME-VITIMA(2)
    #VITIMAZ(#L) := #NOME-VITIMA(3)
    #VITIMAZ(#M) := #NOME-VITIMA(4)
    FOR #X = 4 TO 2 STEP -1
      IF #OBSERVACOES(#X) NE ' '
        COMPUTE #W = #X - 1
        FOR #Y = #W TO 1 STEP -1
          IF #OBSERVACOES(#Y) = ' '
            REINPUT
              'Existe espaco nas linhas acima das que voce informou'
              MARK *#OBSERVACOES(#Y) ALARM
          END-IF
        END-FOR
      END-IF
    END-FOR
    IF #OBSERVACOES(1) = ' '
      ESCAPE BOTTOM
    END-IF
  END-FOR
* END-IF
  END-SUBROUTINE
**
  DEFINE SUBROUTINE VER-STATUS
  DECIDE ON FIRST VALUE OF #STATUS0
    VALUE 1
      #DESCREG := 'REGISTRO ATIVO'
    VALUE 2
      #DESCREG := 'REGISTRO EM BAIXA'
    VALUE 3
      #DESCREG := 'REGISTRO CANCELADO'
    VALUE 4
      #DESCREG := 'LIVRAMENTO CONDICIONAL'
    VALUE 6
      #DESCREG := 'EXTINCAO DA PUNIBILIDADE'
    VALUE 7
      #DESCREG := 'MANDADOS DE PRISAO'
    NONE
      IGNORE
  END-DECIDE
  END-SUBROUTINE
**
  SET CONTROL 'N'
  INPUT NO ERASE ALARM 21/30
    'CONFIRMACAO..(S/N).:'(I) #CONF(AD=AIT'_')
  SET KEY PF3 ENTR
  IF NOT (*PF-KEY = 'PF3' OR = 'ENTR')
    REINPUT 'Tecla invalida' ALARM
  END-IF
  IF *PF-KEY = 'PF3'
    BACKOUT TRANSACTION
    FETCH 'SCRP1201'
  END-IF
  IF #CONF = ' '
    REINPUT 'Confirme "S" ou "N"' MARK *#CONF ALARM
  END-IF
  IF NOT #CONF = 'S' OR = 'N'
    REINPUT 'Confirmacao invalida' MARK *#CONF ALARM
  END-IF
  IF #CONF = 'N'
    BACKOUT TRANSACTION
    FETCH 'SCRP1201'
  ELSE
    IF #LOOP = 1
      SET CONTROL 'WC08L50B33/20F'
      INPUT  (IP=OFF SG=OFF) ALARM
        02/01 'Informe: Matricula:' #MAT(AD=AIL'_')
        'SENHA:' #SENHA (AD=NT)
        03/01 'Para Confirmar a Operacao Tecle ENTRA,'
        04/01 'ou PF3 para Sair'
      IF *PF-KEY = 'PF3'
        INPUT NO ERASE ALARM 05/03
          'ATUALIZACAO NAO CONFIRMADA - TECLE <ENTER>'(I)
        SET CONTROL 'WB'
        BACKOUT TRANSACTION
        FETCH 'SCRP1201'
      END-IF
      IF #MAT = 0
        REINPUT 'Informe a Matricula' MARK *#MAT   ALARM
      END-IF
      IF #MAT NE #MATRICULA#
        REINPUT 'Matricula nao corresponde a Matricula Inicial'
          MARK *#MAT ALARM
      END-IF
      FS1. FIND(1) SENHA-VIEW WITH SEN-MATRICULA EQ #MAT
          #SENHA# := SENHA-VIEW.SENHA-DIGITADA
      END-FIND
      IF #SENHA = ' '
        REINPUT 'Informe sua Senha'     MARK *#SENHA ALARM
      END-IF
      IF #SENHA NE #SENHA#
        REINPUT 'Senha nao Corresponde a Senha Inicial'
          MARK *#SENHA ALARM
      END-IF
      END TRANSACTION
      INPUT NO ERASE ALARM 05/01
        'REGISTRO ALTERADO COM SUCESSO - TECLE <ENTER>' (I)
      RESET  DNAS MNAS ANAS #SENHA
      SET CONTROL 'WB'
    ELSE
      END TRANSACTION
      SET CONTROL 'WC06L35B33/20F'
      INPUT  (IP=OFF SG=OFF) ALARM
        01/01 'REGISTRO ALTERADO COM SUCESSO'(I)
        02/08 'TECLE <ENTER>' (I)
      RESET  DNAS MNAS ANAS #SENHA
      SET CONTROL 'WB'
    END-IF
* ----------------------------
  END-IF
  RESET #VARIAVEIS_DA_TELA #CHAVE
  MOVE (AD=I) TO #CV-DATA #CV-REG-GER #CV-MAE #CV-NOME #VAR
INCLUDE SCRCERRO
END-REPEAT
**
**************************
DEFINE SUBROUTINE CRIMINAL
**************************
SET KEY ALL
SET KEY PF1='%W>'
SET KEY PF2='%W<'
EXIBIDO := FALSE
RESET #INFORMACOES-GERAIS #CHAVE-COMB-2A21 #CHAVE-COMB-2A22
  #CHAVE-COMB-3A31 #CHAVE-COMB-3A32 #CHAVE-COMB-3A33
  #CHAVE-COMB-4A41 #CHAVE-COMB-4A42 #CHAVE-COMB-4A43
  #CHAVE-COMB-4A44 #CHAVE-COMB-4A45 #CHAVE-COMB-4A46
  #FONETO1(*) /* #FONETO2(*)
  #K #EXISTE-SEC  #CHAVE-COMBINA
***********************
**
  RESET #CHAVE-FONETICA
  #CONSULTA       := FALSE
  #TEXTO-CONSULTA := #NOME-COMPLETO
  CALLNAT 'NATBNX22' #CHAVE-FONETICA
  CRIMINAL-VIEW.COD-FONETICO-PRINCIPAL     := #TEXTO-FONETICO(1)
  CRIMINAL-VIEW.COD-FONETICO-SECUNDARIO(*) := #TEXTO-FONETICO(*)
**
RESET #INFORMACOES-GERAIS #CHAVE-COMB-2A21 #CHAVE-COMB-2A22
  #CHAVE-COMB-3A31 #CHAVE-COMB-3A32 #CHAVE-COMB-3A33
  #CHAVE-COMB-4A41 #CHAVE-COMB-4A42 #CHAVE-COMB-4A43
  #CHAVE-COMB-4A44 #CHAVE-COMB-4A45 #CHAVE-COMB-4A46
  #FONETO1(*) /* #FONETO2(*)
  #K #EXISTE-SEC  #CHAVE-COMBINA
**
  RESET #CHAVE-FONETICA-MAE
  #CONSULTA-MAE       := FALSE
  #TEXTO-CONSULTA-MAE := #MAE
  CALLNAT 'NATBNX22' #CHAVE-FONETICA-MAE
  CRIMINAL-VIEW.COD-FONETICO-PRINCIPAL-MAE := #TEXTO-FONETICO-MAE(1)
  CRIMINAL-VIEW.COD-FONETICO-SECUNDARIO-MAE(*):= #TEXTO-FONETICO-MAE(*)
**
********************
  RESET #CHAVE-FONETICA
  #CONSULTA       := TRUE
  #TEXTO-CONSULTA := #NOME-COMPLETO
  CALLNAT 'NATBNX22' #CHAVE-FONETICA
**
  RESET #CHAVE-FONETICA-MAE
  #CONSULTA-MAE       := TRUE
  #TEXTO-CONSULTA-MAE := #MAE
  CALLNAT 'NATBNX22' #CHAVE-FONETICA-MAE
******** INICIO ****
**
RESET #FLAG-CRIMINAL
**
FIND CRIMINAIS WITH SUP-REG-FEDERAL-UF-CI = #SUPER-ID
                   AND  STATUS-REG NE 5
  MOVE 1 TO #FLAG-CRIMINAL
END-FIND
*
FIND CRIMINAIS WITH COD-FONETICO-SECUNDARIO
                                     EQ #TEXTO-FONETICO(01)
     AND COD-FONETICO-SECUNDARIO     EQ #TEXTO-FONETICO(02)
     AND COD-FONETICO-SECUNDARIO     EQ #TEXTO-FONETICO(03)
     AND COD-FONETICO-SECUNDARIO     EQ #TEXTO-FONETICO(04)
     AND COD-FONETICO-SECUNDARIO     EQ #TEXTO-FONETICO(05)
     AND COD-FONETICO-SECUNDARIO     EQ #TEXTO-FONETICO(06)
     AND COD-FONETICO-SECUNDARIO     EQ #TEXTO-FONETICO(07)
     AND COD-FONETICO-SECUNDARIO     EQ #TEXTO-FONETICO(08)
     AND COD-FONETICO-SECUNDARIO     EQ #TEXTO-FONETICO(09)
     AND COD-FONETICO-SECUNDARIO     EQ #TEXTO-FONETICO(10)
     AND COD-FONETICO-SECUNDARIO-MAE EQ #TEXTO-FONETICO-MAE(01)
     AND COD-FONETICO-SECUNDARIO-MAE EQ #TEXTO-FONETICO-MAE(02)
     AND COD-FONETICO-SECUNDARIO-MAE EQ #TEXTO-FONETICO-MAE(03)
     AND COD-FONETICO-SECUNDARIO-MAE EQ #TEXTO-FONETICO-MAE(04)
     AND COD-FONETICO-SECUNDARIO-MAE EQ #TEXTO-FONETICO-MAE(05)
     AND COD-FONETICO-SECUNDARIO-MAE EQ #TEXTO-FONETICO-MAE(06)
     AND COD-FONETICO-SECUNDARIO-MAE EQ #TEXTO-FONETICO-MAE(07)
     AND COD-FONETICO-SECUNDARIO-MAE EQ #TEXTO-FONETICO-MAE(08)
     AND COD-FONETICO-SECUNDARIO-MAE EQ #TEXTO-FONETICO-MAE(09)
     AND COD-FONETICO-SECUNDARIO-MAE EQ #TEXTO-FONETICO-MAE(10)
     AND STATUS-REG NE 5
*
* ------ VERIFICANDO SE O NOME DO INDIVIDUO E O NOME DA MÃE SÃO
* ------ HOMÔNIMOS.
*
* ------ INDIVIDUO
*
     RESET #FLAGN
     FOR #I = 1 TO 10
       IF COD-FONETICO-SECUNDARIO(#I) EQ ' '
          ESCAPE BOTTOM
       END-IF
       IF COD-FONETICO-SECUNDARIO(#I) NE
          #TEXTO-FONETICO(#I)
          ASSIGN #FLAGN = 1
          ESCAPE BOTTOM
       END-IF
     END-FOR
* ------
*
* ------ MAE
     RESET #FLAGM
     FOR #I = 1 TO 10
       IF COD-FONETICO-SECUNDARIO-MAE(#I) EQ ' '
          ESCAPE BOTTOM
       END-IF
       IF COD-FONETICO-SECUNDARIO-MAE(#I) NE
          #TEXTO-FONETICO-MAE(#I)
          ASSIGN #FLAGM = 1
          ESCAPE BOTTOM
       END-IF
     END-FOR
* ------
*
* -- VERIFICA SE A MAE E O INDIVIDUO SÃO HOMONIMOS
*
     IF #FLAGM NE 1 AND #FLAGN NE 1
        MOVE 1 TO #FLAG-CRIMINAL
        ESCAPE BOTTOM
     END-IF
END-FIND
**
FIND CRIMINAIS WITH COD-FONETICO-SECUNDARIO
                                 EQ #TEXTO-FONETICO(01)
     AND COD-FONETICO-SECUNDARIO EQ #TEXTO-FONETICO(02)
     AND COD-FONETICO-SECUNDARIO EQ #TEXTO-FONETICO(03)
     AND COD-FONETICO-SECUNDARIO EQ #TEXTO-FONETICO(04)
     AND COD-FONETICO-SECUNDARIO EQ #TEXTO-FONETICO(05)
     AND COD-FONETICO-SECUNDARIO EQ #TEXTO-FONETICO(06)
     AND COD-FONETICO-SECUNDARIO EQ #TEXTO-FONETICO(07)
     AND COD-FONETICO-SECUNDARIO EQ #TEXTO-FONETICO(08)
     AND COD-FONETICO-SECUNDARIO EQ #TEXTO-FONETICO(09)
     AND COD-FONETICO-SECUNDARIO EQ #TEXTO-FONETICO(10)
     AND DATA-NASC = DDDD
     AND STATUS-REG NE 5
*
* ------ VERIFICANDO SE É MESMO O INDIVIDUO
*
     RESET #FLAGN
     FOR #I = 1 TO 10
       IF COD-FONETICO-SECUNDARIO(#I) EQ ' '
          ESCAPE BOTTOM
       END-IF
       IF COD-FONETICO-SECUNDARIO(#I) NE #TEXTO-FONETICO(#I)
          ASSIGN #FLAGN = 1
          ESCAPE BOTTOM
       END-IF
     END-FOR
*
     IF #FLAGN NE 1
        MOVE 1 TO #FLAG-CRIMINAL
        ESCAPE BOTTOM
     END-IF
END-FIND
**
FIND CRIMINAIS WITH COD-FONETICO-SECUNDARIO-MAE
                   EQ #TEXTO-FONETICO-MAE(01)
     AND COD-FONETICO-SECUNDARIO-MAE EQ #TEXTO-FONETICO-MAE(02)
     AND COD-FONETICO-SECUNDARIO-MAE EQ #TEXTO-FONETICO-MAE(03)
     AND COD-FONETICO-SECUNDARIO-MAE EQ #TEXTO-FONETICO-MAE(04)
     AND COD-FONETICO-SECUNDARIO-MAE EQ #TEXTO-FONETICO-MAE(05)
     AND COD-FONETICO-SECUNDARIO-MAE EQ #TEXTO-FONETICO-MAE(06)
     AND COD-FONETICO-SECUNDARIO-MAE EQ #TEXTO-FONETICO-MAE(07)
     AND COD-FONETICO-SECUNDARIO-MAE EQ #TEXTO-FONETICO-MAE(08)
     AND COD-FONETICO-SECUNDARIO-MAE EQ #TEXTO-FONETICO-MAE(09)
     AND COD-FONETICO-SECUNDARIO-MAE EQ #TEXTO-FONETICO-MAE(10)
     AND DATA-NASC = DDDD
     AND STATUS-REG NE 5
*
* ------ MAE
     RESET #FLAGM
     FOR #I = 1 TO 10
       IF COD-FONETICO-SECUNDARIO-MAE(#I) EQ ' '
          ESCAPE BOTTOM
       END-IF
       IF COD-FONETICO-SECUNDARIO-MAE(#I) NE
          #TEXTO-FONETICO-MAE(#I)
          ASSIGN #FLAGM = 1
          ESCAPE BOTTOM
       END-IF
     END-FOR
* ------
*
     IF #FLAGM NE 1
        MOVE 1 TO #FLAG-CRIMINAL
        ESCAPE BOTTOM
     END-IF
END-FIND
**
** IF *NUMBER(0701) = 0
 IF #FLAG-CRIMINAL = 0
    MOVE 'N' TO ACHOU
    IF NOT EXIBIDO
      INPUT WINDOW='ATESTADO' (AD=OIL'_' IP=OFF SG=OFF)
      02/01 'NENHUM REGISTRO ENCONTRADO NO ARQUIVO CRIMINAL COM:' (AD=I)
      03/01 'NOME: '  (AD=I)
      03/08  #NOME-COMPLETO  (AD=I)
      04/01 'MAE:  ' (AD=I)
      04/08  #MAE  (AD=I)
      MOVE TRUE TO EXIBIDO
    END-IF
 ELSE
* --     write #super-id 
* --CALLNAT 'SCRHOM' #SUPER-ID              #TEXTO-FONETICO(*) 
* 
* --> Nova rotina de homonimos - VERONICA - 30.08.2016 
  CALLNAT 'SCRHOMON' #SUPER-ID              #TEXTO-FONETICO(*) 
                     #TEXTO-FONETICO-MAE(*) DDDD #PROGRAMA# 
    #flag-criminal #nao-pode-imprimir #reg-criminal /* veronica 31.05.17 
* <-- 
    MOVE 'S' tO ACHOU
 END-IF
END-SUBROUTINE
**
DEFINE WINDOW ESP
  SIZE 6*52 BASE 15/25 TITLE ' INFORME SENHA ESPECIAL '
  CONTROL WINDOW FRAMED POSITION SYMBOL LEFT
**
DEFINE WINDOW ATESTADO
  SIZE 8*75 BASE 15/2 TITLE ' CONSULTA '
  CONTROL WINDOW FRAMED POSITION SYMBOL LEFT
END
