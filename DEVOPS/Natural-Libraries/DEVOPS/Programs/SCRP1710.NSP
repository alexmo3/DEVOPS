* >Natural Source Header 000000
* :Mode S
* :CP
* :LineIncrement 10
* <Natural Source Header
* ************************************************************
* SISTEMA    : Sistema de Identificacao Criminal             *
* PROGRAMA   : SCRP1710                                      *
* O QUE FAZ  : IMPRIME ESTATISTICA CRIMINAL                  *
* AUTOR      : MARIA GORETE MEDEIROS                         *
* DATA       : JUNHO DE 1999                                 *
*            : MIGRAÇÃO - FEV/2011 - CZ                      *
* ************************************************************
DEFINE DATA
GLOBAL USING SCRG001
LOCAL  USING SISLDAT8
LOCAL  USING SCRTABEL
LOCAL
1 CRIMINAL VIEW OF SIC-CADASTRO
  2 SEXO
  2 NATURALIDADE
  2 UF-NATURALIDADE
  2 DATA-ATUACAO
  2 NOME-VITIMA  (A60/1)
  2 STATUS-REG
  2 DATA-NASC
  2 LEGISLACAO
  2 OBS (A80/1)
  2 COD-MUNIC-REG-GERAL
  2 MUNICIPIO-REG-GERAL
  2 SUPER-LEGISLACAO-MUNICIPIO
1 #DTF              (N08)
1 TABELA            (N03)
1 #DTI              (N08)
1 #CONT-ET          (N08)
1 NAO-ENCONTROU     (L)
1 LEI-ANT           (A02)
1 MUNIC-ANT         (N04)
1 SUPER-LEI-MUNIC   (A06)
1 REDEFINE SUPER-LEI-MUNIC
  2 LEI             (A02)
  2 MUNIC           (N04)
1 #IDADE            (N04)
1 #SUPERINT         (A60)
1 #STATUS           (A06)
1 #FAIXA-ETARIA     (A12)
1 #DATA_NASC      (N08)
1 REDEFINE  #DATA_NASC
  2 ANAS          (N04)
  2 MNAS          (N02)
  2 DNAS          (N02)
1 #IMP            (A08)
1 #OBSERVACOES (A77/1)
1 REDEFINE #OBSERVACOES
  2 LEI-ART
    3 #OBSERVACOES-LEI(A30)
    3 #OBSERVACOES-ART(A30)
    3 #OBSERVACOES-FIL(A17)
1 #DESC-LEI    (A60)
1 #NOME-VITIMA (A60/1)
1 REDEFINE #NOME-VITIMA
  2 VIT1
    3 #VITIMA1        (A59)
    3 #SEXO-VITIMA1   (A01)
1 #DESC-SEXO           (A01)
1 #DATA-AUTUACAO       (N08)
1 REDEFINE  #DATA-AUTUACAO
  2 #ANO-AUTUACAO      (N04)
  2 #MES-AUTUACAO      (N02)
  2 #DIA-AUTUACAO      (N02)
1 #SEXO                (A01)
1 #NATURALIDADE        (A35)
1 TAB-INI              (N03)
1 TAB-FIM              (N03)
1 #CMD                 (A70)
END-DEFINE
**
ON ERROR
  IF *ERROR-NR = 1573 OR *ERROR-NR = 1559
    SET CONTROL 'WC07L28FNB12/35'
    INPUT WITH TEXT 'IMPRESSORA INVALIDA.' ALARM
      2/2 'IMPRESSORA:' #IMP (AD=MI'_')
      4/2 'PF3 - RETORNA.'
    IF *PF-KEY = 'PF3'
      SET CONTROL 'WB'
      FETCH 'SCRP1700'
    END-IF
    IF NOT (*PF-KEY = 'PF3' OR = 'ENTR')
      REINPUT 'TECLA DE FUNCAO INVALIDA' MARK *#IMP ALARM
    END-IF
    SET CONTROL 'WB'
    FETCH 'SCRP1710' DATINI DATFIM TAB-INI TAB-FIM LEI #IMP
  ELSE
    WRITE 'TRANSACAO CANCELADA, ENTRAR EM CONTATO COM C.P.D.'
    WRITE 'ERROR NUMERO:' *ERROR-NR ' LINHA:' *ERROR-LINE
      ' PROGRAMA:' *PROGRAM
  END-IF
END-ERROR
SET KEY ALL
INPUT DATINI DATFIM TAB-INI TAB-FIM LEI #IMP
** CC DEFINE PRINTER (1) OUTPUT #IMP
DEFINE PRINTER (1) OUTPUT 'LPT1'
FORMAT (1) LS=132 PS=60 SG=OFF
FOR TABELA = TAB-INI TO TAB-FIM
  LEI-ANT := 0
  MUNIC-ANT := 0
  #CONT-ET  := 0
  TAB := 299
  COD := TABELA
  FIND (1) VW-TABELE WITH DISTAB-TABELE = PESQ
    IF NO RECORD FOUND
      INPUT WINDOW='ESP' (AD=OIL'_' IP=OFF SG=OFF)
        01/01 'SUPERINTENDENCIA NAO CADASTRADA: ' (AD=I)
        02/01 'CODIGO: ' TABELA        (AD=I)
        04/01 'ENTER - CONTINUA'
        04/20 'PF3 - RETORNA'
      IF *PF-KEY = 'PF3'
        SET CONTROL 'WB'
        FETCH 'SCRP1700'
      END-IF
    END-NOREC
    MOVE DIATAB-DESCEL TO #SUPERINT
  END-FIND
  RD2. READ CRIMINAL WITH SUPER-LEGISLACAO-MUNICIPIO = SUPER-LEI-MUNIC
      WHERE  STATUS-REG NE 5
    COMPUTE #CONT-ET     = #CONT-ET + 1
    IF #CONT-ET = 50
      SET CONTROL 'N'
      INPUT NO ERASE 23/01 'PROCESSANDO...'
      END TRANSACTION
      RESET #CONT-ET
    END-IF
    IF LEGISLACAO GT LEI
      ESCAPE BOTTOM (RD2.)
    END-IF
    IF DATA-ATUACAO LT DATINI OR DATA-ATUACAO GT DATFIM
      ESCAPE TOP
    END-IF
    IF COD-MUNIC-REG-GERAL NE MUNIC-ANT
      TAB := TABELA
      COD := COD-MUNIC-REG-GERAL
      FIND (1) VW-TABELE WITH DISTAB-TABELE = PESQ
      END-FIND
      IF *NUMBER(0142) = 0
        NAO-ENCONTROU := TRUE
        ESCAPE TOP
      ELSE
        NAO-ENCONTROU := FALSE
      END-IF
    ELSE
      IF NAO-ENCONTROU = TRUE
        ESCAPE TOP
      END-IF
    END-IF
*
    IF LEGISLACAO NE LEI-ANT
      NEWPAGE (1)
      MOVE CRIMINAL.LEGISLACAO TO #OBSERVACOES(1)
      PERFORM DESC-LEI
      WRITE (1) USING FORM 'SCRR1700'
      LEI-ANT := LEGISLACAO
    END-IF
*
    DECIDE ON FIRST VALUE OF SEXO
      VALUE 1
        #SEXO := 'M'
      VALUE 2
        #SEXO := 'F'
      VALUE 3
        #SEXO := 'S'
      NONE
        IGNORE
    END-DECIDE
    DECIDE ON FIRST VALUE OF STATUS-REG
      VALUE 1
        #STATUS := 'ATIVO'
      VALUE 2
        #STATUS := 'BAIXA'
      VALUE 3
        #STATUS := 'CANC'
      VALUE 4
        #STATUS := 'LIVR'
      VALUE 6
        #STATUS := 'EXTI'
      VALUE 7
        #STATUS := 'MAND'
      NONE
        IGNORE
    END-DECIDE
    #DATA-AUTUACAO :=  CRIMINAL.DATA-ATUACAO
    #DATA_NASC :=  CRIMINAL.DATA-NASC
    PERFORM CALCULA-IDADE
    IF #IDADE LT 18
      MOVE 'MENOS DE 18' TO  #FAIXA-ETARIA
    ELSE
      IF (#IDADE GE 18)  AND (#IDADE LE 35)
        MOVE 'DE 18 A 35' TO  #FAIXA-ETARIA
      ELSE
        IF #IDADE GT 35
          MOVE 'MAIS DE 35' TO  #FAIXA-ETARIA
        END-IF
      END-IF
    END-IF
    MOVE CRIMINAL.NOME-VITIMA(1) TO #NOME-VITIMA(1)
    #DESC-SEXO := #SEXO-VITIMA1
    PERFORM DESC-SEXO
    #SEXO-VITIMA1 := #DESC-SEXO
    MOVE CRIMINAL.OBS(1) TO #OBSERVACOES(1) /* PEGA ARTIGO
    WRITE (1) NOTITLE
      02T CRIMINAL.MUNICIPIO-REG-GERAL
      1X  #STATUS
      1X  #OBSERVACOES-ART
      1X #SEXO
      5X  #FAIXA-ETARIA
      2X  CRIMINAL.NATURALIDADE (AL=25)
      2X  CRIMINAL.UF-NATURALIDADE
      2X  #SEXO-VITIMA1
  END-READ
END-FOR
CLOSE PRINTER (1)
include printer1
FETCH 'SCRP1700'
**
DEFINE SUBROUTINE DESC-LEI
DECIDE ON FIRST VALUE OF #OBSERVACOES-LEI
  VALUE '1'
    MOVE '1 - CONTRAVENCOES PENAIS' TO #DESC-LEI
  VALUE '2'
    MOVE '2 - CRIMES CONTRA A PESSOA' TO #DESC-LEI
  VALUE '3'
    MOVE '3 - CRIMES CONTRA O PATRIMONIO' TO #DESC-LEI
  VALUE '4'
    MOVE '4 - CRIMES CONTRA A PROP. IMATERIAL' TO #DESC-LEI
  VALUE '5'
    MOVE '5 - CRIMES CONTRA A ORG. DO TRABALHO' TO #DESC-LEI
  VALUE '6'
    MOVE '6 - CRIMES CONTRA O SENT. REL. E CONTRA O RESP. AOS MORTOS'
      TO #OBSERVACOES-LEI
  VALUE '7'
    MOVE '7 - CRIMES CONTRA OS COSTUMES' TO #DESC-LEI
  VALUE '8'
    MOVE '8 - CRIMES CONTRA A FAMILIA' TO #DESC-LEI
  VALUE '9'
    MOVE '9 - CRIMES CONTRA A INCOL. PUBLICA' TO #DESC-LEI
  VALUE '10'
    MOVE '10 - CRIMES CONTRA A PAZ PUBLICA' TO #DESC-LEI
  VALUE '11'
    MOVE '11 - CRIMES CONTRA A FE PUBLICA' TO #DESC-LEI
  VALUE '12'
    MOVE '12 - CRIMES CONTRA A ADM. PUBLICA' TO #DESC-LEI
  VALUE '13'
    MOVE '13 - Outras' TO #OBSERVACOES-LEI
  NONE
    IGNORE
END-DECIDE
END-SUBROUTINE
**************************
DEFINE SUBROUTINE DESC-SEXO
DECIDE ON FIRST VALUE OF #DESC-SEXO
  VALUE '1'
    #DESC-SEXO := 'M'
  VALUE '2'
    #DESC-SEXO := 'F'
  VALUE '3'
    #DESC-SEXO := 'S'
  NONE
    IGNORE
END-DECIDE
END-SUBROUTINE
*******************************
DEFINE SUBROUTINE CALCULA-IDADE
*******************************
COMPUTE #IDADE = #ANO-AUTUACAO - ANAS
IF MNAS GT #MES-AUTUACAO
  COMPUTE #IDADE = #IDADE - 1
ELSE
  IF MNAS = #MES-AUTUACAO AND DNAS GT #DIA-AUTUACAO
    COMPUTE #IDADE = #IDADE - 1
  END-IF
END-IF
END-SUBROUTINE
*******************************
DEFINE WINDOW ESP
  SIZE 8*38 BASE 12/25 TITLE ' ATENCAO '
  CONTROL WINDOW FRAMED POSITION SYMBOL BOTTOM LEFT
END
