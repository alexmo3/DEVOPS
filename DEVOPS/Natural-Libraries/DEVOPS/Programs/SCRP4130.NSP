* >Natural Source Header 000000
* :Mode S
* :CP
* :LineIncrement 10
* <Natural Source Header
* !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *
* SISTEMA    : Sistema de Identificacao Criminal             *
* PROGRAMA   : SCRP4130 - CONSOLIDACAO DE LOTES              *
* AUTOR      : ANALICE                                       *
* DATA       : MARCO 2004                                    *
* !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *
DEFINE DATA
GLOBAL USING SCRG001
LOCAL USING SCRLSENH
LOCAL USING SCRLINFJ /* SCR-INFOSEG-REJEICOES
LOCAL
1 INF-REM1 VIEW OF SCR-INFOSEG-REMESSA
  2 SUPER-DATA-SEQUENCIA
  2 DT-REMESSA
  2 SQ-REMESSA
  2 HR-REMESSA
  2 TT-INCLUSAO
  2 TT-ALTERACAO
  2 TT-EXCLUSAO
  2 TT-INCLUSAO-NOMES
  2 TT-EXCLUSAO-NOMES
  2 ST-REMESSA
1 INF-REM VIEW OF SCR-INFOSEG-REMESSA
  2 DT-REMESSA
  2 SQ-REMESSA
  2 TT-INCLUSAO
  2 TT-ALTERACAO
  2 TT-EXCLUSAO
  2 TT-INCLUSAO-NOMES
  2 TT-EXCLUSAO-NOMES
  2 ST-REMESSA
1 CRIM VIEW OF SIC-CADASTRO
  2 DT-REMESSA-INFOSEG
  2 SQ-REMESSA-INFOSEG
  2 REGISTRO-ESTADUAL-GERAL
  2 FL-REMESSA-INFOSEG
  2 FL-REMESSA-APELIDO-INFOSEG
  2 CD-REGISTRO-GERAL
  2 IG-UF
1 #REG-GERAL           (N11)
1 #CD-REGISTRO (A2)
1 REDEFINE #CD-REGISTRO
  2 #CD-SITUACAO (A1)
  2 #CD-OPERACAO (A1)
1 #X-REG-GERAL         (A20)
1 REDEFINE #X-REG-GERAL
  2 #X-REG-GERALN       (N20)
1 ESTOURO (N04)
1 #ISN    (P08)
* 1 ATUAL   (A01)
1 #MSG    (A48)
1 QTDETR10  (N06) INIT<0>
1 RTDETR10  (N06) INIT<0>
1 QTDETR11  (N06) INIT<0>
1 RTDETR11  (N06) INIT<0>
1 QTDETR12  (N06) INIT<0>
1 RTDETR12  (N06) INIT<0>
1 QTDETR13  (N06) INIT<0>
1 RTDETR13  (N06) INIT<0>
1 QTDETR14  (N06) INIT<0>
1 RTDETR14  (N06) INIT<0>
1 WFL(A20)
1 REDEFINE WFL
  2 WFL-DATA (A8)
  2 WFL-SEQ  (A2)
  2 WFL-10   (A2)
  2 WFL-11   (A2)
  2 WFL-12   (A2)
  2 WFL-13   (A2)
  2 WFL-14   (A2)
1 WDT-REMESSA   (A08)
1 REDEFINE WDT-REMESSA
  2 WDT-REMESSAN   (N08)
1 WSQ-REMESSA   (A02)
1 XX-ERRO   (A96)
1 REDEFINE XX-ERRO
  2  XX-ERRO1  (A70)
  2  XX-ERRO2A (A26)
1 DATA-ATUAL (N08)
1 REDEFINE DATA-ATUAL
  2 DATA-ATUAL-AA (N04)
  2 DATA-ATUAL-MM (N02)
  2 DATA-ATUAL-DD (N02)
1 #NHR-ULT-REM (N8)
1 REDEFINE #NHR-ULT-REM
  2 #HR-ULT-REM (A8)
  2 REDEFINE #HR-ULT-REM
    3 #HR-ULT-REM6 (A6)
    3 #HR-UTL-REM2 (A2)
1 DATAALF (A10)
1 XD-REMESSA  (A10)
1 REDEFINE XD-REMESSA
  2  XD-REMESSA-DATA(A08)
  2 REDEFINE XD-REMESSA-DATA
    3 ND-REMESSA-DATA (N08)
    3 REDEFINE ND-REMESSA-DATA
      4 ND-REMESSA-ANO (N04)
      4 ND-REMESSA-MES (N02)
      4 ND-REMESSA-DIA (N02)
  2  XD-REMESSA-SEQ (A02)
  2 REDEFINE XD-REMESSA-SEQ
    3 ND-REMESSA-SEQ (N2)
1 XD-STATUS (A1)
1 #CONF (A1)
END-DEFINE
* MOVE ' 'TO ATUAL
INCLUDE SISCPFS
* **********************************
* FAZ CONSOLIDACAO DO LOTE
* **********************************
READ INF-REM1 BY SUPER-DATA-SEQUENCIA
  MOVE INF-REM1.DT-REMESSA        TO XD-REMESSA-DATA
  MOVE XD-REMESSA-DATA   TO WDT-REMESSA
  MOVE XD-REMESSA-SEQ    TO WSQ-REMESSA
  PERFORM SISSD002 ND-REMESSA-DATA
  MOVE EDITED ND-REMESSA-DATA(EM=99/99/9999) TO DATAALF
  MOVE INF-REM1.SQ-REMESSA        TO XD-REMESSA-SEQ
  MOVE INF-REM1.HR-REMESSA        TO #HR-ULT-REM6
  MOVE INF-REM1.TT-INCLUSAO       TO QTDETR10
  MOVE INF-REM1.TT-ALTERACAO      TO QTDETR11
  MOVE INF-REM1.TT-EXCLUSAO       TO QTDETR12
  MOVE INF-REM1.TT-INCLUSAO-NOMES TO QTDETR13
  MOVE INF-REM1.TT-EXCLUSAO-NOMES TO QTDETR14
  MOVE INF-REM1.ST-REMESSA        TO XD-STATUS
END-READ
IF INF-REM1.ST-REMESSA EQ 'C'
  RESET #CONF
  SET CONTROL 'WC15L45B20/45F'
  INPUT USING MAP 'SCRM4131'
  SET CONTROL 'WB'
ELSE
  MOVE 'N' TO #CONF
  SET CONTROL 'WC14L50B20/45F'
  INPUT USING MAP 'SCRM4130'
  SET CONTROL 'WB'
  IF #CONF EQ 'S'
    PERFORM PERCORRE-REJEICAO
    IF #CONF EQ 'S'
      PERFORM ATUALIZA-REMESSA
      PERFORM ATUALIZA-CADASTRO-ACEITOS
* **  PERFORM ZERA-REJEICAO
    END-IF
  END-IF
END-IF
SET CONTROL 'WB'
FETCH 'SCRP4100'
***************************************
* PERCORRE REJEICAO E ATUALIZA CADASTRO CRIMINAL
DEFINE SUBROUTINE PERCORRE-REJEICAO
***************************************
RESET ESTOURO
R0. READ INF-REJ BY NU-IG-UF
  MOVE RIGHT JUSTIFIED INF-REJ.NU-IG-UF    TO #X-REG-GERAL
  IF INF-REJ.TP-REGISTRO EQ  '10'
    ADD 1 TO RTDETR10
  END-IF
  IF INF-REJ.TP-REGISTRO EQ  '11'
    ADD 1 TO RTDETR11
  END-IF
  IF INF-REJ.TP-REGISTRO EQ  '12'
    ADD 1 TO RTDETR12
  END-IF
  IF INF-REJ.TP-REGISTRO EQ  '13'
    ADD 1 TO RTDETR13
  END-IF
  IF INF-REJ.TP-REGISTRO EQ  '14'
    ADD 1 TO RTDETR14
  END-IF
  MOVE *ISN TO #ISN
  F2. FIND(1) CRIM WITH REGISTRO-ESTADUAL-GERAL EQ #X-REG-GERALN
    MOVE CRIM.CD-REGISTRO-GERAL TO #CD-REGISTRO
    IF (TP-REGISTRO EQ '10' OR EQ '11' OR EQ '12')
      MOVE 'R' TO FL-REMESSA-INFOSEG
    ELSE
      MOVE 'R' TO FL-REMESSA-APELIDO-INFOSEG
    END-IF
    MOVE 'C' TO #CD-SITUACAO
    MOVE #CD-REGISTRO TO CRIM.CD-REGISTRO-GERAL
    UPDATE RECORD(F2.)
    END TRANSACTION
  END-FIND
  IF *COUNTER(F2.) GT 0
    G1. GET INF-REJ #ISN
    MOVE 'C' TO INF-REJ.FL-TRATAMENTO /* REGISTRO CONSOLIDADO
    UPDATE RECORD(G1.)
  END-IF
  END TRANSACTION
  ESTOURO := ESTOURO + 1
  IF ESTOURO EQ 300
    SET CONTROL 'N'
    INPUT NO ERASE 'PERCORRENDO REJEICOES. AGUARDE ...' 22/01
    RESET ESTOURO
    END TRANSACTION
  END-IF
  SET CONTROL 'WB'
END-READ
IF *COUNTER(R0.) EQ 0
  MOVE 'N' TO #CONF
  SET CONTROL 'WC10L50B20/45F'
  INPUT USING MAP 'SCRM4132'
  SET CONTROL 'WB'
END-IF
END-SUBROUTINE
* ******************************
*  ATUALIZA REMESSA
DEFINE SUBROUTINE ATUALIZA-REMESSA
* ******************************
* SPLAY WDT-REMESSA WSQ-REMESSA
  R3. READ INF-REM BY DT-REMESSA STARTING FROM WDT-REMESSA  THRU
    WDT-REMESSA
  ACCEPT IF INF-REM.SQ-REMESSA EQ XD-REMESSA-SEQ
   COMPUTE
   INF-REM.TT-INCLUSAO = INF-REM.TT-INCLUSAO - RTDETR10
   COMPUTE
   INF-REM.TT-ALTERACAO = INF-REM.TT-ALTERACAO - RTDETR11
   COMPUTE
  INF-REM.TT-EXCLUSAO = INF-REM.TT-EXCLUSAO - RTDETR12
  COMPUTE
  INF-REM.TT-INCLUSAO-NOMES = INF-REM.TT-INCLUSAO-NOMES - RTDETR13
  COMPUTE
  INF-REM.TT-EXCLUSAO-NOMES = INF-REM.TT-EXCLUSAO-NOMES - RTDETR14
  MOVE 'C' TO INF-REM.ST-REMESSA
  UPDATE (R3.)
  END TRANSACTION
END-READ
END-SUBROUTINE
* ***************************
DEFINE SUBROUTINE ZERA-REJEICAO /* ZERA REGISTROS CONSOLIDADOS
*******************************
RESET ESTOURO
READ INF-REJ BY NU-IG-UF
  ACCEPT IF INF-REJ.FL-TRATAMENTO NE ' '
  DELETE RECORD
  ESTOURO := ESTOURO + 1
  IF ESTOURO EQ 100
    RESET ESTOURO
    END TRANSACTION
  END-IF
END-READ
END TRANSACTION
END-SUBROUTINE
* ******************************************
DEFINE SUBROUTINE ATUALIZA-CADASTRO-ACEITOS
* ******************************************
R4. READ CRIM BY DT-REMESSA-INFOSEG  STARTING
    FROM WDT-REMESSAN THRU WDT-REMESSAN
  ADD 1 TO ESTOURO
  IF ESTOURO EQ 300
    SET CONTROL 'N'
    INPUT NO ERASE 'ATUALIZANDO CADASTRO. AGUARDE ...' 22/01
    END TRANSACTION
    RESET ESTOURO
  END-IF
  ACCEPT IF SQ-REMESSA-INFOSEG EQ XD-REMESSA-SEQ
  MOVE '00' TO WFL-10 WFL-11 WFL-12 WFL-13 WFL-14
  MOVE CD-REGISTRO-GERAL TO #CD-REGISTRO
  IF #CD-SITUACAO EQ 'E'
    MOVE WDT-REMESSA  TO WFL-DATA
    MOVE WSQ-REMESSA  TO WFL-SEQ
    IF FL-REMESSA-INFOSEG EQ 'I'
      MOVE '10' TO WFL-10
      MOVE ' ' TO FL-REMESSA-INFOSEG
    END-IF
    IF FL-REMESSA-INFOSEG EQ 'X'
      MOVE '12' TO WFL-12
      MOVE ' ' TO FL-REMESSA-INFOSEG
    END-IF
    IF FL-REMESSA-INFOSEG EQ 'A'
      MOVE '11' TO WFL-11
      MOVE ' ' TO FL-REMESSA-INFOSEG
    END-IF
    IF FL-REMESSA-APELIDO-INFOSEG EQ 'I'
      MOVE '13' TO WFL-13
      MOVE ' ' TO FL-REMESSA-APELIDO-INFOSEG
    END-IF
    IF FL-REMESSA-APELIDO-INFOSEG EQ 'X'
      MOVE '14' TO WFL-14
      MOVE ' ' TO FL-REMESSA-APELIDO-INFOSEG
    END-IF
      MOVE'C' TO #CD-SITUACAO
    MOVE WFL TO IG-UF
    MOVE #CD-REGISTRO TO CD-REGISTRO-GERAL
    UPDATE (R4.)
  END-IF
END-READ
END TRANSACTION
END-SUBROUTINE
* ************
END
