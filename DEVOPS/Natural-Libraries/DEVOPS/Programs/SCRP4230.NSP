* >Natural Source Header 000000
* :Mode S
* :CP
* :LineIncrement 10
* <Natural Source Header
* !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *
* SISTEMA    : Sistema de Identificacao Criminal             *
* PROGRAMA   : SCRP4230 - CONSULTA ARQUIVO DE MOVIMENTACOES
* AUTOR      : ANALICE                                       *
* DATA       : MAIO  2002                                    *
* !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *
DEFINE DATA
GLOBAL USING SCRG001
LOCAL USING SCRLSENH
LOCAL USING SCRLINFM
LOCAL
1 #EMPRESA   (A08) INIT <' CODATA '>
1 #PROGRAM   (A08) INIT  <*PROGRAM>
1 #FUNC      (A10) INIT <'INFOSEG'>
1 VTIPO-REG  (A02/1:2)
1 VNOME      (A70/1:2)
1 VREG-GERAL (A20/1:2)
1 VMAE       (A70/1:2)
1 VPAI       (A70/1:2)
1 VDATANASC  (A10/1:2)
1 VSEXO      (A01/1:2)
1 VCUTIS     (A02/1:2)
1 VNATURAL   (A35/1:2)
1 VNATURALUF (A02/1:2)
1 WDATA     (A08)
1 REDEFINE WDATA
  2  W-ANO   (A4)
  2  W-MES   (A2)
  2  W-DIA   (A2)
1 WDATAINVN  (N08)
1 REDEFINE WDATAINVN
  2 WDATAINV (A08)
  2 REDEFINE WDATAINV
    3  WI-DIA (A2)
    3  WI-MES (A2)
    3  WI-ANO (A4)
1 I  (N02)
1 ESTOURO (N06)
1 ATUAL   (A01)
END-DEFINE
INCLUDE SISCPFS
I := 0
READ SCR-INFOSEG-MOV-VIEW BY NU-IG-UF
  PERFORM MONTA-TELA
  AT END OF DATA
    IF I >= 1
      INPUT MAP 'SCRM4230'
      RESET VTIPO-REG(*) VNOME(*) VREG-GERAL(*) VPAI(*) VMAE(*) VSEXO(*)
        VDATANASC(*) VCUTIS(*) VNATURAL(*) VNATURALUF(*)
    END-IF
  END-ENDDATA
END-READ
IF *COUNTER(0048) = 0
*  SET CONTROL
  INPUT NO ERASE 20/30 'NAO HA DADOS DA MOVIMENTACAO...TECLE.<ENTER>'
END-IF
FETCH 'SCRP4200'
* ***************************
DEFINE SUBROUTINE MONTA-TELA
*******************************
ADD 1 TO I
MOVE SCR-INFOSEG-MOV-VIEW.TP-REGISTRO   TO VTIPO-REG(I)
MOVE SCR-INFOSEG-MOV-VIEW.NU-IG-UF      TO VREG-GERAL(I)
MOVE SCR-INFOSEG-MOV-VIEW.NM-NOME       TO VNOME(I)
MOVE SCR-INFOSEG-MOV-VIEW.NM-PAI        TO VPAI(I)
MOVE SCR-INFOSEG-MOV-VIEW.NM-MAE        TO VMAE(I)
MOVE SCR-INFOSEG-MOV-VIEW.DT-NASCIMENTO TO WDATA
MOVE W-ANO TO WI-ANO
MOVE W-MES TO WI-MES
MOVE W-DIA TO WI-DIA
MOVE EDITED WDATAINVN(EM=99/99/9999) TO VDATANASC(I)
MOVE SCR-INFOSEG-MOV-VIEW.CD-SEXO            TO VSEXO(I)
MOVE SCR-INFOSEG-MOV-VIEW.CD-CUTIS           TO VCUTIS(I)
MOVE SCR-INFOSEG-MOV-VIEW.NM-NATURALIDADE    TO VNATURAL(I)
MOVE SCR-INFOSEG-MOV-VIEW.SG-UF-NATURALIDADE TO VNATURALUF(I)
IF I = 2
  INPUT MAP 'SCRM4230'
  I:= 0
  RESET VTIPO-REG(*) VNOME(*) VREG-GERAL(*) VPAI(*) VMAE(*) VSEXO(*)
    VDATANASC(*) VCUTIS(*) VNATURAL(*) VNATURALUF(*)
END-IF
IF *PF-KEY =  'PF3'
  FETCH 'SCRP4200'
END-IF
END-SUBROUTINE
END
