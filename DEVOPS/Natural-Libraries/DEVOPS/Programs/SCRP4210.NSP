* >Natural Source Header 000000
* :Mode S
* :CP
* :LineIncrement 10
* <Natural Source Header
* !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *
* SISTEMA    : Sistema de Identificacao Criminal             *
* PROGRAMA   : SCRP4210 - CONSULTA REMESSAS                  *
* AUTOR      : ANALICE                                       *
* DATA       : MAIO  2003                                    *
* !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *
DEFINE DATA
GLOBAL USING SCRG001
LOCAL USING SCRLSENH
LOCAL
1 INF-REM VIEW OF SCR-INFOSEG-REMESSA
  2 SUPER-DATA-SEQUENCIA
  2 DT-REMESSA
  2 SQ-REMESSA
  2 HR-REMESSA
  2 ST-REMESSA
  2 TT-INCLUSAO
  2 TT-ALTERACAO
  2 TT-EXCLUSAO
  2 TT-INCLUSAO-NOMES
  2 TT-EXCLUSAO-NOMES
1 #EMPRESA   (A08) INIT <' CODATA '>
1 #PROGRAM   (A08) INIT  <*PROGRAM>
1 #FUNC      (A10) INIT <'INFOSEG'>
1 VDATA      (A10/1:13)
1 VSEQ       (A02/1:13)
1 VHORA      (A06/1:13)
1 VSTATUS    (A13/1:13)
1 VQTDE10    (N06/1:13)
1 VQTDE11    (N06/1:13)
1 VQTDE12    (N06/1:13)
1 VQTDE13    (N06/1:13)
1 VQTDE14    (N06/1:13)
1 VDATAALF   (A08)
1 REDEFINE VDATAALF
  2  VDATANUM (N8)
1 I  (N02)
1 ESTOURO (N06)
1 ATUAL   (A01)
1 WCOMANDO         (A5)
1 WIMPRESSORA      (A8)
1 REDEFINE WIMPRESSORA
  2 #IMP             (A4)
END-DEFINE
* **************************
INCLUDE SISCPFS
I := 0
READ INF-REM BY SUPER-DATA-SEQUENCIA
 PERFORM MONTA-TELA
 AT END OF DATA
    IF I >= 1
    INPUT USING MAP 'SCRM4210'
    END-IF
 IF *PF-KEY = 'PF11'
    PERFORM PEDE-IMPRESSORA
    IF *PF-KEY NE 'PF3'
      WRITE(1) NOTITLE USING FORM 'SCRF4210'
       CLOSE PRINTER(1)
   END-IF
 END-IF
   RESET VDATA(*) VSEQ(*) VQTDE10(*) VQTDE11(*) VQTDE12(*) VQTDE13(*)
         VQTDE14(*) VHORA(*) VSTATUS(*)
 END-ENDDATA
END-READ
IF *COUNTER(0053) = 0
   INPUT NO ERASE 20/30 'NAO HA DADOS DA MOVIMENTACAO...TECLE.<ENTER>'
END-IF
   FETCH 'SCRP4200'
* ***************************
DEFINE SUBROUTINE MONTA-TELA
*******************************
ADD 1 TO I
MOVE INF-REM.SQ-REMESSA         TO VSEQ(I)
MOVE INF-REM.HR-REMESSA         TO VHORA(I)
MOVE INF-REM.TT-INCLUSAO        TO VQTDE10(I)
MOVE INF-REM.TT-ALTERACAO       TO VQTDE11(I)
MOVE INF-REM.TT-EXCLUSAO        TO VQTDE12(I)
MOVE INF-REM.TT-INCLUSAO-NOMES  TO VQTDE13(I)
MOVE INF-REM.TT-EXCLUSAO-NOMES  TO VQTDE14(I)
MOVE INF-REM.DT-REMESSA         TO VDATAALF
DECIDE ON FIRST VALUE OF INF-REM.ST-REMESSA
 VALUE 'E'
     MOVE 'ENVIADO' TO VSTATUS(I)
 VALUE 'C'
     MOVE 'CONSOLIDADO' TO VSTATUS(I)
 NONE
 MOVE '*' TO VSTATUS(I)
END-DECIDE
PERFORM SISSD002 VDATANUM
MOVE EDITED VDATANUM(EM=99/99/9999) TO VDATA(I)
IF I = 13
   INPUT USING MAP 'SCRM4210'
   I:= 0
END-IF
IF *PF-KEY =  'PF3'
    FETCH 'SCRP4200'
END-IF
 IF *PF-KEY = 'PF11'
    PERFORM PEDE-IMPRESSORA
    IF *PF-KEY NE 'PF3'
       WRITE(1) NOTITLE USING FORM 'SCRF4210'
       CLOSE PRINTER(1)
   END-IF
 END-IF
END-SUBROUTINE
* ***************************************
DEFINE SUBROUTINE PEDE-IMPRESSORA
 SET CONTROL 'WC07L27FNB12/35'
 INPUT  WITH TEXT 'INFORME IMPRESSORA.' ALARM
 4/2 'IMPRESSORA:' #IMP (AD=MI'_')
 6/2 'PF3 - RETORNA '
 IF #IMP = ' ' AND *PF-KEY = 'ENTR'
    REINPUT 'INFORME IMPRESSORA.' MARK *#IMP ALARM
 END-IF
 IF *PF-KEY NE 'PF3'
     END TRANSACTION
     SET CONTROL 'WB'
     COMPRESS 'H' #IMP INTO WCOMANDO LEAVING NO SPACE
     DEFINE PRINTER (1) OUTPUT WIMPRESSORA
     FORMAT (1) LS=80 PS=60 EJECT OFF
 END-IF
END-SUBROUTINE
END
