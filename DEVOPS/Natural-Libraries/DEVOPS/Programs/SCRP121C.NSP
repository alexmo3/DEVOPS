* >Natural Source Header 000000
* :Mode S
* :CP
* :LineIncrement 10
* <Natural Source Header
* !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *
* SISTEMA    : Sistema de Identificacao Criminal             *
* PROGRAMA   : SCRP1201                                      *
* O QUE FAZ  ? ALTERACAO DA FICHA INDICE CRIMINAL REDUZIDA   *
* AUTOR      : MARIA GORETE M S CRUZ                         *
* DATA       : JULHO DE 1999                                 *
* !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *
*
DEFINE DATA
GLOBAL USING SCRG001
LOCAL  USING SCRVARIA
LOCAL  USING SCRLSENH
LOCAL  USING SCRLNUME
LOCAL
*****
1 CRIMINAIS VIEW OF SIC-CADASTRO
  2 CODIGO-FONETICO-PRINCIPAL
  2 CODIGO-FONETICO-SECUNDARIO     (1:61)
  2 CODIGO-FONETICO-PRINCIPAL-MAE
  2 CODIGO-FONETICO-SECUNDARIO-MAE (1:61)
  2 STATUS-REG
  2 REGISTRO-ESTADUAL-GERAL
  2 DATA-NASC
1 MATESP                (N07)
1 SENESP                (A06)
1 #NIVEL-AUT            (A01)
1 #EMPRESA              (A08) INIT <' CODATA '>
1 #PROGRAM              (A08) INIT <*PROGRAM>
1 #FUNCAO               (A10) INIT <'ALTERACAO'>
1 CHAVE                 (A01) INIT <'A'>
1 DMANAS                (A08)
1 REDEFINE DMANAS
  2 DNAS                (N02)
  2 MNAS                (N02)
  2 ANAS                (N04)
1 DDDD                  (N08)
1 #DATA-ALT-STATUS      (N08)
1 REDEFINE #DATA-ALT-STATUS
  2 #ANO-ALT-STATUS     (N04)
  2 #MES-ALT-STATUS     (N02)
  2 #DIA-ALT-STATUS     (N02)
1 #ST-OLD               (N01)
1 #ULT-NUM              (N11)
1 #MAE-LIMPA            (A60)
1 #INFRACAO             (C)
1 #VAR-ST               (C)
1 #IMP                  (A08)
1 #MSGCRIM              (A60)
**** VARIAVEIS DA ROT-FONETICA
1 #EXISTE-SEC           (A01)
1 #NUMERO2              (N05)
1 #SEMELHANTE           (A01)
1 #J2                   (N02)
1 #K                    (N02)
1 #SEC                  (A06)
1 #SEC-ANT              (A06)
1 #AUX                  (A06)
1 #CHAVE-COMBINA        (A120)
1 REDEFINE #CHAVE-COMBINA
  2 #VET                      (A06/1:20)
1 #FONETO1                    (A06/1:61)
1 #FONETO-NOME-DADO           (A06)
1 REDEFINE #FONETO-NOME-DADO
  2 FONETO-NOME-DADO          (B06)
1 #FONETO-MAE-DADO            (A06)
1 REDEFINE #FONETO-MAE-DADO
  2 FONETO-MAE-DADO           (B06)
1 #INFORMACOES-GERAIS         (A97)
1 REDEFINE #INFORMACOES-GERAIS
  2 #NOME-DA-PESSOA           (A80)
  2 #TAMANHO-NOME-PESSOA      (B02)
  2 #TIPO-NOME                (A01)
  2 #NUMERO-COMPONENTES       (B02)
  2 #TIPO-COMBINACAO          (A01)
  2 #NUMERO-COMBINACOES       (B02)
  2 #INICIO-COMBINACAO        (B02)
  2 #CHAVE-NOME-DADO          (A06)
  2 REDEFINE #CHAVE-NOME-DADO
    3 CHAVE-NOME-DADO         (B06)
  2 #FLAG                     (A01)
**1 #FILLER                  (A100)
1 #CHAVE-COMB-2A21            (A240)
1 #CHAVE-COMB-2A22            (A030)
1 #CHAVE-COMB-3A31            (A240)
1 #CHAVE-COMB-3A32            (A240)
1 #CHAVE-COMB-3A33            (A240)
1 #CHAVE-COMB-4A41            (A240)
1 #CHAVE-COMB-4A42            (A240)
1 #CHAVE-COMB-4A43            (A240)
1 #CHAVE-COMB-4A44            (A240)
1 #CHAVE-COMB-4A45            (A240)
1 #CHAVE-COMB-4A46            (A240)
END-DEFINE
*
RESET #CONTADOR# DNAS MNAS ANAS #VARIAVEIS_DA_TELA #ST-OLD
FORMAT PS=23
SET KEY ALL
#PROGRAMA# := *PROGRAM
#FUNC := 'ALTERACAO'
SET CONTROL 'MB'
MOVE (AD=I) TO #VAR #CV-MAE #CV-NOME #VAR-ST
IF NOT (*USER = '18GO' OR = '18go')
MOVE (AD=IP) TO #VAR-ST
END-IF
REPEAT
  #VLT# := 2
  #CHV  := '*'
  ADD 1 TO #LOOP
  #OCORRENCIA := 1
  INPUT USING MAP 'SCRM2000'
  FD33. FIND (1) CRIMINAL-VIEW WITH
    REGISTRO-ESTADUAL-GERAL = #REG_GERAL
    IF *COUNTER(FD33.) GT 0
      IF STATUS-REG = 5
       REINPUT 'Esta ficha esta" excluida - Nao pode ser alterada' ALARM
      END-IF
      MOVE CRIMINAL-VIEW.DATA-NASC TO #DATA_NASC00
      #DIA_NASC := DNAS    := #DIA_NASC0
      #MES_NASC := MNAS    := #MES_NASC0
      #ANO_NASC := ANAS    := #ANO_NASC0
      MOVE CRIMINAL-VIEW.DATA-REG TO #DATA-REGISTRO0
      #A-REG := #A-REG0
      #M-REG := #M-REG0
      #D-REG := #D-REG0
      MOVE  CRIMINAL-VIEW.REGISTRO-ESTADUAL-GERAL  TO #REG_GERAL
      MOVE  CRIMINAL-VIEW.STATUS-REG               TO #STATUS0 #ST-OLD
      MOVE  CRIMINAL-VIEW.DATA-ALTERACAO-STATUS    TO #DATA-ALT-STATUS
      MOVE  CRIMINAL-VIEW.NOME-COMPLETO            TO #NOME-COMPLETO
      MOVE  CRIMINAL-VIEW.NOME-PAI                 TO #PAI
      MOVE  CRIMINAL-VIEW.NOME-MAE                 TO #MAE
      MOVE  CRIMINAL-VIEW.MUNICIPIO-REG-GERAL      TO #DESC-MUNICIPIO
      MOVE  CRIMINAL-VIEW.COD-MUNIC-REG-GERAL      TO #MUNICIPIO
      MOVE  CRIMINAL-VIEW.UF-REG-ESTADUAL-GERAL    TO #UF-REG-GERAL
      MOVE  CRIMINAL-VIEW.DATA-ATUACAO             TO #DATA-ATUACAO
      MOVE  CRIMINAL-VIEW.APELIDO-ALCUNHAS         TO #ALCUNHA
      MOVE  CRIMINAL-VIEW.NATURALIDADE             TO #NATURALIDADE
     MOVE  CRIMINAL-VIEW.COD-MUNICIPIO-NASCIMENTO TO #COD-MUN-NASCIMENTO
      MOVE  CRIMINAL-VIEW.UF-NATURALIDADE          TO #UF-NASCIMENTO
      MOVE  CRIMINAL-VIEW.NOME-VITIMA(*)           TO #VITIMAZ(*)
      MOVE  CRIMINAL-VIEW.OBS(*)                   TO #OBSERVACOEZ(*)
      #OBSERVACOES(1) := #OBSERVACOEZ(1)
      #OBSERVACOES(2) := #OBSERVACOEZ(2)
      #OBSERVACOES(3) := #OBSERVACOEZ(3)
      #OBSERVACOES(4) := #OBSERVACOEZ(4)
      #NOME-VITIMA(1) := #VITIMAZ(1)
      #NOME-VITIMA(2) := #VITIMAZ(2)
      #NOME-VITIMA(3) := #VITIMAZ(3)
      #NOME-VITIMA(4) := #VITIMAZ(4)
      PERFORM VER-STATUS
*
      MOVE (AD=IP) TO #CV-REG-GER
      INPUT USING MAP 'SCRM1201'
      IF *PF-KEY = 'PF3'
        FETCH 'SCRP1201'
      END-IF
*
      IF #ST-OLD NE #STATUS0
        SET CONTROL 'N'
        INPUT NO ERASE 21/25 'CONFIRMA ALTERACAO DE STATUS?' (AD=I)
          #CONF (AD=AIT'_')
        SET KEY PF3 ENTR
        IF NOT (*PF-KEY = 'PF3' OR = 'ENTR')
          REINPUT 'Tecla invalida' ALARM
        END-IF
        IF *PF-KEY = 'PF3'
          FETCH 'SCRP1201'
        END-IF
        IF #CONF = ' '
          REINPUT 'Confirme "S" ou "N"' MARK *#CONF ALARM
        END-IF
        IF NOT #CONF = 'S' OR = 'N'
          REINPUT 'Confirmacao invalida' MARK *#CONF ALARM
        END-IF
        IF #CONF = 'N'
          MOVE #ST-OLD TO #STATUS0
          INPUT MAP 'SCRM1201' NO ERASE
        ELSE
          PERFORM VER-STATUS
          MOVE *DATN TO #DATA-ALT-STATUS
*         INPUT MAP 'SCRM1201' NO ERASE
 INPUT WITH TEXT 'TECLE ENTER PARA CONTINUAR' MAP 'SCRM1201' NO ERASE
        END-IF
      END-IF
****
      MOVE DMANAS TO #DATA_NASC
      COMPUTE DDDD = (ANAS*10000) + (MNAS*100) + DNAS
      PERFORM CRIMINAL
*
      MOVE (AD=IP) TO #CV-REG-GER #INFRACAO #CV-IDENT #CV-NOME
        #CV-MAE #CV-DATA
      MOVE *DATN         TO #DATA_ATU
      MOVE *TIME         TO #TEMPO
      MOVE #TEMPOHORA    TO #HORA
      MOVE #TEMPOMIN     TO #MIN
      MOVE #TEMPOSEG     TO #SEG
      MOVE #TEMPONSE     TO #NSE
      MOVE #DATA_NASC    TO #DATA_NASC0
      MOVE #DIA_NASC     TO #DIA_NASC0
      MOVE #MES_NASC     TO #MES_NASC0
      MOVE #ANO_NASC     TO #ANO_NASC0
      MOVE #D-REG        TO #D-REG0
      MOVE #M-REG        TO #M-REG0
      MOVE #A-REG        TO #A-REG0
      REPEAT WHILE #CHV = '*'
        IF #OBSERVACOES(1) = ' '
          REINPUT 'NENHUMA OCORRENCIA FOI INFORMADA.'
            MARK *#OBSERVACOES(1) ALARM
        ELSE
          RESET #CHV
        END-IF
      END-REPEAT
      PERFORM GRAVA-OCORRENCIAS
*
      IF #MAE = '*' OR = ' '
        MOVE '************' TO #MAE
      END-IF
      IF #PAI = '*' OR = ' '
        MOVE '************' TO #PAI
      END-IF
      IF #ALCUNHA     = ' ' OR = '*'
        MOVE '************' TO #ALCUNHA
      END-IF
      IF #DATA-ATUACAO LT 1
        MOVE  0 TO #DATA-ATUACAO
      END-IF
*
      CRIMINAL-VIEW.OBS(*)                   := #OBSERVACOEZ(*)
      CRIMINAL-VIEW.LEGISLACAO               := #OBSERVACOEZ(1)
      CRIMINAL-VIEW.NOME-VITIMA(*)           := #VITIMAZ(*)
      CRIMINAL-VIEW.REGISTRO-ESTADUAL-GERAL  := #REG_GERAL
      CRIMINAL-VIEW.STATUS-REG               := #STATUS0
      CRIMINAL-VIEW.DATA-ALTERACAO-STATUS    := #DATA-ALT-STATUS
      CRIMINAL-VIEW.TIPO-REG                 := 'AL'
      CRIMINAL-VIEW.NOME-COMPLETO            := #NOME-COMPLETO
      CRIMINAL-VIEW.NOME-PAI                 := #PAI
      CRIMINAL-VIEW.NOME-MAE                 := #MAE
      CRIMINAL-VIEW.DATA-NASC                := #DATA_NASC00
      CRIMINAL-VIEW.MUNICIPIO-REG-GERAL      := #DESC-MUNICIPIO
      CRIMINAL-VIEW.COD-MUNIC-REG-GERAL      := #MUNICIPIO
      CRIMINAL-VIEW.UF-REG-ESTADUAL-GERAL    := #UF-REG-GERAL
      CRIMINAL-VIEW.DATA-REG                 := #DATA-REGISTRO0
      CRIMINAL-VIEW.DATA-ATUACAO             := #DATA-ATUACAO
      CRIMINAL-VIEW.APELIDO-ALCUNHAS         := #ALCUNHA
      CRIMINAL-VIEW.DATDOC-ATU               := #DATA_ATU
      CRIMINAL-VIEW.HORA-ATU                 := #HORA_ATU
      CRIMINAL-VIEW.MATFUNC-ATU              := #MATRICULA#
* ------------------------------------------------ *
      UPDATE(FD33.)
    END-IF
  END-FIND
  IF *COUNTER(FD33.) EQ 0
    BACKOUT TRANSACTION
    REINPUT 'Ficha Criminal nao cadastrada' ALARM
  END-IF
  DEFINE SUBROUTINE GRAVA-OCORRENCIAS
  #I := 5
  IF #OBSERVACOES(1) NE ' '
    IF NOT (*PF-KEY = 'PF3' OR = 'ENTR')
      REINPUT 'Tecla invalida' ALARM
    END-IF
    #OBSERVACOEZ(1) := #OBSERVACOES(1) /* PEGA ALTERACOES DAS 4  1AS.
    #OBSERVACOEZ(2) := #OBSERVACOES(2) /* OCORRENCIAS JA MOSTRADAS
    #OBSERVACOEZ(3) := #OBSERVACOES(3)
    #OBSERVACOEZ(4) := #OBSERVACOES(4)
    #VITIMAZ(1) := #NOME-VITIMA(1)
    #VITIMAZ(2) := #NOME-VITIMA(2)
    #VITIMAZ(3) := #NOME-VITIMA(3)
    #VITIMAZ(4) := #NOME-VITIMA(4)
  END-IF
* FOR #OCORRENCIA = 2 TO 40
* FOR #OCORRENCIA = 2 TO 25
  FOR #OCORRENCIA = 2 TO 20
    IF #OCORRENCIA = 2
      COMPUTE #J = #OCORRENCIA + 4
      COMPUTE #L = #OCORRENCIA + 5
      COMPUTE #M = #OCORRENCIA + 6
    ELSE
      COMPUTE #I = #M + 1
      COMPUTE #J = #M + 2
      COMPUTE #L = #M + 3
      COMPUTE #M = #M + 4
    END-IF
    #OBSERVACOES(1)  := #OBSERVACOEZ(#I)
    #OBSERVACOES(2)  := #OBSERVACOEZ(#J)
    #OBSERVACOES(3)  := #OBSERVACOEZ(#L)
    #OBSERVACOES(4)  := #OBSERVACOEZ(#M)
    #NOME-VITIMA(1)  := #VITIMAZ(#I)
    #NOME-VITIMA(2)  := #VITIMAZ(#J)
    #NOME-VITIMA(3)  := #VITIMAZ(#L)
    #NOME-VITIMA(4)  := #VITIMAZ(#M)
    INPUT MAP 'SCRM1201'
**    INPUT NO ERASE
**      14/01 'INCIDENCIAS..:' #OCORRENCIA(AD=OIL)
**      15/05 ' ' #OBSERVACOES-LEI (AD=MIL'_')
**      15/48 ' ' #OBSERVACOES-ART (AD=MIL'_')
**      16/01 ' ' #OBSERVACOES(2)(AD=MIL'_')
**      17/01 ' ' #OBSERVACOES(3)(AD=MIL'_')
**      18/01 ' ' #OBSERVACOES(4)(AD=MIL'_')
    SET KEY PF3 ENTR
    IF NOT (*PF-KEY = 'PF3' OR = 'ENTR')
      REINPUT 'Tecla invalida' ALARM
    END-IF
    IF *PF-KEY = 'PF3'
      FETCH 'SCRP1201'
    END-IF
*     IF #OBSERVACOES(1) = ' '
*        ESCAPE BOTTOM
*     END-IF
    #OBSERVACOEZ(#I) := #OBSERVACOES(1)
    #OBSERVACOEZ(#J) := #OBSERVACOES(2)
    #OBSERVACOEZ(#L) := #OBSERVACOES(3)
    #OBSERVACOEZ(#M) := #OBSERVACOES(4)
*     RESET #OBSERVACOES(*)
    #VITIMAZ(#I) := #NOME-VITIMA(1)
    #VITIMAZ(#J) := #NOME-VITIMA(2)
    #VITIMAZ(#L) := #NOME-VITIMA(3)
    #VITIMAZ(#M) := #NOME-VITIMA(4)
    FOR #X = 4 TO 2 STEP -1
      IF #OBSERVACOES(#X) NE ' '
        COMPUTE #W = #X - 1
        FOR #Y = #W TO 1 STEP -1
          IF #OBSERVACOES(#Y) = ' '
            REINPUT
              'Existe espaco nas linhas acima das que voce informou'
              MARK *#OBSERVACOES(#Y) ALARM
          END-IF
        END-FOR
      END-IF
    END-FOR
    IF #OBSERVACOES(1) = ' '
      ESCAPE BOTTOM
    END-IF
  END-FOR
* END-IF
  END-SUBROUTINE
  DEFINE SUBROUTINE VER-STATUS
  DECIDE ON FIRST VALUE OF #STATUS0
    VALUE 1
      #DESCREG := 'REGISTRO ATIVO'
    VALUE 2
      #DESCREG := 'REGISTRO EM BAIXA'
    VALUE 3
      #DESCREG := 'REGISTRO CANCELADO'
    VALUE 4
      #DESCREG := 'LIVRAMENTO CONDICIONAL'
    NONE
      IGNORE
  END-DECIDE
  END-SUBROUTINE
* ------------------------------------------------ *
  SET CONTROL 'N'
  INPUT NO ERASE ALARM 21/30
    'CONFIRMACAO..(S/N).:'(I) #CONF(AD=AIT'_')
  SET KEY PF3 ENTR
  IF NOT (*PF-KEY = 'PF3' OR = 'ENTR')
    REINPUT 'Tecla invalida' ALARM
  END-IF
  IF *PF-KEY = 'PF3'
    BACKOUT TRANSACTION
    FETCH 'SCRP1201'
  END-IF
  IF #CONF = ' '
    REINPUT 'Confirme "S" ou "N"' MARK *#CONF ALARM
  END-IF
  IF NOT #CONF = 'S' OR = 'N'
    REINPUT 'Confirmacao invalida' MARK *#CONF ALARM
  END-IF
  IF #CONF = 'N'
    BACKOUT TRANSACTION
    FETCH 'SCRP1201'
  ELSE
    IF #LOOP = 1
      SET CONTROL 'WC08L50B33/20F'
      INPUT  (IP=OFF SG=OFF) ALARM
        02/01 'Informe: Matricula:' #MAT(AD=AIL'_')
        'Senha:' #SENHA(AD=ANT)
        03/01 'Para Confirmar a Operacao Tecle ENTRA,'
        04/01 'ou PF3 para Sair'
      IF *PF-KEY = 'PF3'
        INPUT NO ERASE ALARM 05/03
          'ATUALIZACAO NAO CONFIRMADA - TECLE <ENTER>'(I)
        SET CONTROL 'WB'
        BACKOUT TRANSACTION
        FETCH 'SCRP1201'
      END-IF
      IF #MAT = 0
        REINPUT 'Informe a Matricula' MARK *#MAT   ALARM
      END-IF
      IF #MAT NE #MATRICULA#
        REINPUT 'Matricula nao corresponde a Matricula Inicial'
          MARK *#MAT ALARM
      END-IF
      IF #SENHA = ' '
        REINPUT 'Informe sua Senha'     MARK *#SENHA ALARM
      END-IF
      IF #SENHA NE #SENHA#
        REINPUT 'Senha nao Corresponde a Senha Inicial'
          MARK *#SENHA ALARM
      END-IF
      END TRANSACTION
      INPUT NO ERASE ALARM 05/01
        'REGISTRO ALTERADO COM SUCESSO - TECLE <ENTER>' (I)
      RESET  DNAS MNAS ANAS DEDOS(*,*)
      SET CONTROL 'WB'
    ELSE
      END TRANSACTION
      SET CONTROL 'WC06L35B33/20F'
      INPUT  (IP=OFF SG=OFF) ALARM
        01/01 'REGISTRO ALTERADO COM SUCESSO'(I)
        02/08 'TECLE <ENTER>' (I)
      RESET  DNAS MNAS ANAS DEDOS(*,*)
      SET CONTROL 'WB'
    END-IF
* ----------------------------
  END-IF
  RESET #VARIAVEIS_DA_TELA #CHAVE #TEXT
  MOVE (AD=I) TO #CV-DATA #CV-REG-GER #CV-MAE #CV-NOME #VAR
  INCLUDE SCRCERRO
END-REPEAT
*******************************
DEFINE SUBROUTINE ROT-FONETICA
******************************
MOVE +60 TO #TAMANHO-NOME-PESSOA
MOVE 'F' TO #TIPO-NOME
MOVE +0  TO #NUMERO-COMPONENTES
MOVE '8' TO #TIPO-COMBINACAO
MOVE +20 TO #NUMERO-COMBINACOES
MOVE +0  TO #INICIO-COMBINACAO
MOVE '0' TO #FLAG
CALL 'PGP60RFO' #INFORMACOES-GERAIS #CHAVE-COMB-2A21 #CHAVE-COMB-2A22
  #CHAVE-COMB-3A31 #CHAVE-COMB-3A32 #CHAVE-COMB-3A33
  #CHAVE-COMB-4A41 #CHAVE-COMB-4A42 #CHAVE-COMB-4A43
  #CHAVE-COMB-4A44 #CHAVE-COMB-4A45 #CHAVE-COMB-4A46
*
MOVE #NUMERO-COMPONENTES TO #NUMERO2
*
IF #NUMERO2 = 2
  ADD 1 TO #K
  MOVE #CHAVE-NOME-DADO TO #FONETO1 (#K)
END-IF
IF #NUMERO2 = 3
  ADD 1 TO #K
  MOVE #CHAVE-COMB-2A21 TO #CHAVE-COMBINA
  ADD 1 TO #K
  MOVE #VET (1)         TO #FONETO1 (#K)
  ADD 1 TO #K
  MOVE #VET (2)         TO #FONETO1 (#K)
  MOVE #CHAVE-COMB-3A31 TO #CHAVE-COMBINA
  ADD 1 TO #K
  MOVE #VET (1)         TO #FONETO1 (#K)
END-IF
*
IF #NUMERO2 = 4
  MOVE #CHAVE-COMB-2A21 TO #CHAVE-COMBINA
  ADD 1 TO #K
  MOVE #VET (1)         TO #FONETO1 (#K)
  ADD 1 TO #K
  MOVE #VET (2)         TO #FONETO1 (#K)
  ADD 1 TO #K
  MOVE #VET (3)         TO #FONETO1 (#K)
  MOVE #CHAVE-COMB-3A31 TO #CHAVE-COMBINA
  FOR #J2 1 TO 20
    MOVE #VET(#J2) TO #AUX
    IF #AUX = ' '
      ESCAPE BOTTOM
    END-IF
    ADD 1 TO #K
    MOVE #VET(#J2) TO #FONETO1 (#K)
  END-FOR
  MOVE #CHAVE-COMB-4A41 TO #CHAVE-COMBINA
  ADD 1 TO #K
  MOVE #VET(1) TO #FONETO1 (#K)
END-IF
*
IF #NUMERO2 GT 4
  MOVE #CHAVE-COMB-2A21 TO #CHAVE-COMBINA
  FOR #J2 1 TO 20
    MOVE #VET(#J2) TO #AUX
    IF #AUX = ' '
      ESCAPE BOTTOM
    END-IF
    ADD 1 TO #K
    MOVE #VET(#J2) TO #FONETO1 (#K)
  END-FOR
*
  MOVE #CHAVE-COMB-3A31 TO #CHAVE-COMBINA
  FOR #J2 1 TO 20
    MOVE #VET(#J2) TO #AUX
    IF #AUX = ' '
      ESCAPE BOTTOM
    END-IF
    ADD 1 TO #K
    MOVE #VET(#J2) TO #FONETO1 (#K)
  END-FOR
*
  MOVE #CHAVE-COMB-4A41 TO #CHAVE-COMBINA
*   MOVE 0 TO #K
  FOR #J2 1 TO 20
    MOVE #VET(#J2) TO #AUX
    IF #AUX = ' '
      ESCAPE BOTTOM
    END-IF
    ADD 1 TO #K
    MOVE #VET(#J2) TO #FONETO1 (#K)
  END-FOR
  ADD 1 TO #K
  MOVE #CHAVE-NOME-DADO TO #FONETO1 (#K)
END-IF
END-SUBROUTINE
**************************
DEFINE SUBROUTINE CRIMINAL
**************************
RESET #INFORMACOES-GERAIS #CHAVE-COMB-2A21 #CHAVE-COMB-2A22
  #CHAVE-COMB-3A31 #CHAVE-COMB-3A32 #CHAVE-COMB-3A33
  #CHAVE-COMB-4A41 #CHAVE-COMB-4A42 #CHAVE-COMB-4A43
  #CHAVE-COMB-4A44 #CHAVE-COMB-4A45 #CHAVE-COMB-4A46
  #FONETO1(*) /* #FONETO2(*)
  #K #EXISTE-SEC  #CHAVE-COMBINA
MOVE #NOME-COMPLETO TO #NOME-DA-PESSOA
PERFORM ROT-FONETICA
MOVE #CHAVE-NOME-DADO TO #FONETO-NOME-DADO
CRIMINAL-VIEW.CODIGO-FONETICO-PRINCIPAL := #CHAVE-NOME-DADO
FOR #I = 1 TO 61
  CRIMINAL-VIEW.CODIGO-FONETICO-SECUNDARIO(#I) := #FONETO1(#I)
END-FOR
********************
RESET #INFORMACOES-GERAIS #CHAVE-COMB-2A21 #CHAVE-COMB-2A22
  #CHAVE-COMB-3A31 #CHAVE-COMB-3A32 #CHAVE-COMB-3A33
  #CHAVE-COMB-4A41 #CHAVE-COMB-4A42 #CHAVE-COMB-4A43
  #CHAVE-COMB-4A44 #CHAVE-COMB-4A45 #CHAVE-COMB-4A46
  #FONETO1(*) /* #FONETO2(*)
  #K #EXISTE-SEC  #CHAVE-COMBINA
MOVE #MAE TO #NOME-DA-PESSOA
PERFORM ROT-FONETICA
MOVE #CHAVE-NOME-DADO TO #FONETO-MAE-DADO
CRIMINAL-VIEW.CODIGO-FONETICO-PRINCIPAL-MAE := #CHAVE-NOME-DADO
FOR #I = 1 TO 61
  CRIMINAL-VIEW.CODIGO-FONETICO-SECUNDARIO-MAE(#I) := #FONETO1(#I)
END-FOR
FIND CRIMINAIS WITH  (CODIGO-FONETICO-PRINCIPAL = FONETO-NOME-DADO
    AND CODIGO-FONETICO-PRINCIPAL-MAE = #FONETO-MAE-DADO
    AND DATA-NASC                     = DDDD )
    OR  (CODIGO-FONETICO-PRINCIPAL = FONETO-NOME-DADO
    AND CODIGO-FONETICO-PRINCIPAL-MAE = #FONETO-MAE-DADO)
    OR  (CODIGO-FONETICO-PRINCIPAL = FONETO-NOME-DADO
    AND DATA-NASC                     = DDDD )
    OR  (DATA-NASC                         = DDDD
    AND CODIGO-FONETICO-PRINCIPAL-MAE = #FONETO-MAE-DADO)
  ACCEPT IF CRIMINAIS.REGISTRO-ESTADUAL-GERAL NE #REG_GERAL
    AND CRIMINAIS.STATUS-REG NE 5
  COMPRESS 'HOMONIMOS CADASTRADOS, R. Criminal:'
    REGISTRO-ESTADUAL-GERAL INTO #MSGCRIM
  INPUT WINDOW='ESP' (AD=MIL'_' IP=OFF SG=OFF)
    WITH TEXT #MSGCRIM ALARM
    02/01 'MATRICULA:' MATESP   'senha:' SENESP (AD=N)
    03/20 'PF3 => Sair'
  IF *PF-KEY = 'PF3'
    FETCH 'SCRP1201'
  END-IF
  IF *PF-KEY NE 'PF3'
    FD2. FIND SENHA-VIEW WITH SEN-MATRICULA = MATESP
      IF SEN-SENHA NE SENESP
        REINPUT 'SENHA INCORRETA' MARK *SENESP ALARM
      END-IF
      MOVE SEN-NIVEL TO #NIVEL-AUT
      IF #NIVEL-AUT LT '6'
        REINPUT 'Usuario nao autorizado'
      END-IF
    END-FIND
    IF *COUNTER(FD2.) = 0
      REINPUT 'Usuario nao cadastrado no sistema' MARK *MATESP ALARM
    END-IF
  END-IF
  RESET MATESP SENESP
END-FIND   /* do criminal
END-SUBROUTINE
*************************************
DEFINE WINDOW ESP
  SIZE 6*52 BASE 15/25 TITLE ' INFORME SENHA ESPECIAL '
  CONTROL WINDOW FRAMED POSITION SYMBOL BOT LEFT
END
