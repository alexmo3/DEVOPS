* >Natural Source Header 000000
* :Mode S
* :CP
* :LineIncrement 10
* <Natural Source Header
* !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *
* SISTEMA    : Sistema de Identificacao Criminal             *
* PROGRAMA   : SCRP4240 - CONSULTA REJEICOES
* AUTOR      : ANALICE                                       *
* DATA       : MARCO 2004                                    *
* !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *
DEFINE DATA
GLOBAL USING SCRG001
LOCAL USING SCRLSENH
LOCAL USING SCRLINFJ
LOCAL
1 #EMPRESA   (A08) INIT <' CODATA '>
1 #PROGRAM   (A08) INIT  <*PROGRAM>
1 #MATRICULA (N07)
1 #SENHA     (A06)
1 #NSENH     (A06)
1 OPC0       (A01/1:5)
1 #FUNC      (A10) INIT <'INFOSEG'>
1 #CURSOR    (N06)
1 VTIPO-REG  (A02/1:2)
1 VNOME      (A70/1:2)
1 VREG-GERAL (A20/1:2)
1 VMAE       (A70/1:2)
1 VPAI       (A70/1:2)
1 VDATANASC  (A10/1:2)
1 VSEXO      (A01/1:2)
1 VCUTIS     (A02/1:2)
1 VNATURAL   (A35/1:2)
1 VNATURALUF (A02/1:2)
1 VERRO      (A70/1:2)
1 VDATANUM   (N08)
1 REDEFINE VDATANUM
  2  VDATAALF (A08)
1 #DATANUM (N08)
1 REDEFINE #DATANUM
  2  #DATAANO (N04)
  2  #DATAMES (N02)
  2  #DATADIA (N02)
1 I  (N02)
 1 XX-ERRO   (A96)
  1 REDEFINE XX-ERRO
  2  XX-ERRO1  (A70)
  2  XX-ERRO2A (A26)
1 ESTOURO (N06)
1 ATUAL   (A01)
END-DEFINE
**************************
*  MONTA HEADER
**************************
INCLUDE SISCPFS
*
* REPEAT
  SET CONTROL 'MB'
  IF *PF-KEY =  'PF3'
    FETCH 'SCRP4200'
  END-IF
I := 0
READ INF-REJ BY NU-IG-UF
PERFORM MONTA-TELA
 AT END OF DATA
   IF I >= 1
     INPUT MAP 'SCRM4240'
     RESET VTIPO-REG(*) VNOME(*) VREG-GERAL(*) VPAI(*) VMAE(*) VSEXO(*)
     VDATANASC(*) VCUTIS(*) VNATURAL(*) VNATURALUF(*) VERRO(*)
   END-IF
 END-ENDDATA
END-READ
* END-REPEAT
    FETCH 'SCRP4200'
* ***************************
DEFINE SUBROUTINE MONTA-TELA
*******************************
ADD 1 TO I
MOVE INF-REJ.TP-REGISTRO   TO VTIPO-REG(I)
MOVE INF-REJ.NU-IG-UF      TO VREG-GERAL(I)
MOVE INF-REJ.NM-NOME       TO VNOME(I)
MOVE INF-REJ.NM-PAI        TO VPAI(I)
MOVE INF-REJ.NM-MAE        TO VMAE(I)
MOVE INF-REJ.DT-NASCIMENTO TO VDATAALF
MOVE VDATANUM TO #DATANUM
IF VDATANUM EQ MASK(YYYYMMDD)
    IF #DATAANO LT 1800 AND INF-REJ.TP-REGISTRO EQ '10'
       MOVE 1900 TO #DATAANO
       MOVE 01   TO #DATAMES
       MOVE 01   TO #DATADIA
    END-IF
    MOVE #DATANUM TO VDATANUM
    PERFORM SISSD002  VDATANUM
END-IF
MOVE EDITED VDATANUM(EM=99/99/9999) TO VDATANASC(I)
MOVE INF-REJ.CD-SEXO            TO VSEXO(I)
MOVE INF-REJ.CD-CUTIS           TO VCUTIS(I)
MOVE INF-REJ.NM-NATURALIDADE    TO VNATURAL(I)
MOVE INF-REJ.SG-UF-NATURALIDADE TO VNATURALUF(I)
MOVE INF-REJ.DS-ERRO            TO XX-ERRO
MOVE XX-ERRO1 TO VERRO(I)
 IF I = 2
   INPUT MAP 'SCRM4240'
   I:= 0
   RESET VTIPO-REG(*) VNOME(*) VREG-GERAL(*) VPAI(*) VMAE(*) VSEXO(*)
          VDATANASC(*) VCUTIS(*) VNATURAL(*) VNATURALUF(*) VERRO(*)
  END-IF
IF *PF-KEY =  'PF3'
   FETCH 'SCRP4200'
END-IF
END-SUBROUTINE
END
