* >Natural Source Header 000000
* :Mode S
* :CP
* :LineIncrement 10
* <Natural Source Header
* !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *
* SISTEMA    : Sistema de Identificacao Criminal             *
* PROGRAMA   : SCRP4320 - IMPRIME ULTIMO MOVIMENTO           *
* AUTOR      : ANALICE                                       *
* DATA       : ABRIL 2004                                    *
* !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *
DEFINE DATA
GLOBAL USING SCRG001
LOCAL USING SCRLSENH
LOCAL
1 MOV VIEW OF SCR-INFOSEG-MOV
  2 TP-REGISTRO
  2 NU-IG-UF
  2 NM-NOME
  2 DT-NASCIMENTO
1 #EMPRESA   (A08) INIT <' CODATA '>
1 #PROGRAM   (A08) INIT  <*PROGRAM>
1 #FUNC      (A10) INIT <'INFOSEG'>
1 VDATA      (A10)
1 WDATA     (A08)
1 REDEFINE WDATA
  2  W-ANO   (A4)
  2  W-MES   (A2)
  2  W-DIA   (A2)
1 WDATAINVN  (N08)
1 REDEFINE WDATAINVN
  2 WDATAINV (A08)
  2 REDEFINE WDATAINV
    3  WI-DIA (A2)
    3  WI-MES (A2)
    3  WI-ANO (A4)
1 WNUM (A20)
1 REDEFINE WNUM
  2 WNUM9 (A9)
  2 WNUM11 (A11)
1 WNOME-COMPLETO (A70)
1 REDEFINE WNOME-COMPLETO
  2 WNOME (A48)
  2 WNOMEFIL (A22)
1 WIMPRESSORA      (A8)
1 REDEFINE WIMPRESSORA
  2 #IMP             (A4)
END-DEFINE
* **************************
MOVE *PROGRAM TO #PROGRAMA#
INCLUDE SISCPFS
PERFORM PEDE-IMPRESSORA
IF *PF-KEY NE 'PF3'
  READ MOV BY NU-IG-UF
    MOVE RIGHT JUSTIFIED MOV.NU-IG-UF TO WNUM
    IF (MOV.TP-REGISTRO = '10' OR = '11')
      MOVE MOV.NM-NOME TO WNOME-COMPLETO
      MOVE MOV.DT-NASCIMENTO TO WDATA
      MOVE W-ANO TO WI-ANO
      MOVE W-MES TO WI-MES
      MOVE W-DIA TO WI-DIA
      MOVE EDITED WDATAINVN(EM=99/99/9999) TO VDATA
    ELSE
      IF MOV.TP-REGISTRO = '13'
        MOVE MOV.NM-NOME TO WNOME-COMPLETO
      END-IF
    END-IF
    WRITE (1)  1T WNUM11 14T MOV.TP-REGISTRO 17T WNOME 70T VDATA
    RESET VDATA WNOME WNUM
  END-READ
  CLOSE PRINTER (1)
END-IF
FETCH 'SCRP4300'
* **********************************
DEFINE SUBROUTINE PEDE-IMPRESSORA
* ********************************
SET CONTROL 'WC07L28FNB12/35'
INPUT WITH TEXT 'INFORME IMPRESSORA.' ALARM
  2/2 'IMPRESSORA:' #IMP (AD=MIT'_')
  4/2 'PF3 - RETORNA.'
IF NOT (*PF-KEY = 'PF3' OR = 'ENTR')
  REINPUT 'TECLA DE FUNCAO INVALIDA' MARK *#IMP ALARM
END-IF
IF #IMP = ' ' AND *PF-KEY = 'ENTR'
  REINPUT 'INFORME IMPRESSORA.' MARK *#IMP ALARM
ELSE
  SET CONTROL 'MB'
  IF *PF-KEY NE 'PF3'
    DEFINE PRINTER (1) OUTPUT WIMPRESSORA
    FORMAT (1) LS=80 PS=60 EJECT OFF
  END-IF
END-IF
SET CONTROL 'WB'
END-SUBROUTINE
AT TOP OF PAGE (1)
  WRITE (1) NOTITLE
    01T 'COMPANHIA DE PROCESSAMENTO DE DADOS - CODATA'
    72T *DATE /
    01T 'SSP-IPC - SISTEMA DE IDENTIFICACAO CRIMINAL' 72T
    'PAG:' *PAGE-NUMBER(1) (EM=999) /
    01T 'INFOSEG' 72T #PROGRAMA# //
    32T 'ULTIMA MOVIMENTACAO' //
    01T 'REG. GERAL TIPO'  17T 'NOME'  70T 'DATA NASC.' /
    01T '='(79)
END-TOPPAGE
END
