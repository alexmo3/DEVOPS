* >Natural Source Header 000000
* :Mode S
* :CP
* :LineIncrement 10
* <Natural Source Header
* !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *
* SISTEMA    : Sistema de Identificacao Criminal             *
* PROGRAMA   : SCRP4310 - IMPRIME REMESSA                    *
* AUTOR      : ANALICE                                       *
* DATA       : ABRIL 2004                                    *
* !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *
DEFINE DATA
GLOBAL USING SCRG001
LOCAL USING SCRLSENH
LOCAL
1 INF-REM VIEW OF SCR-INFOSEG-REMESSA
  2 SUPER-DATA-SEQUENCIA
  2 DT-REMESSA
  2 SQ-REMESSA
  2 HR-REMESSA
  2 ST-REMESSA
  2 TT-INCLUSAO
  2 TT-ALTERACAO
  2 TT-EXCLUSAO
  2 TT-INCLUSAO-NOMES
  2 TT-EXCLUSAO-NOMES
1 #EMPRESA   (A08) INIT <' CODATA '>
1 #PROGRAM   (A08) INIT  <*PROGRAM>
1 #FUNC      (A10) INIT <'INFOSEG'>
1 VDATA      (A10)
1 VSTATUS    (A11)
1 VDATAALF   (A08)
1 REDEFINE VDATAALF
  2  VDATANUM (N8)
1 WIMPRESSORA      (A8)
1 REDEFINE WIMPRESSORA
  2 #IMP             (A4)
END-DEFINE
* **************************
MOVE *PROGRAM TO #PROGRAMA#
INCLUDE SISCPFS
PERFORM PEDE-IMPRESSORA
IF *PF-KEY NE 'PF3'
  READ INF-REM BY SUPER-DATA-SEQUENCIA
    DECIDE ON FIRST VALUE OF INF-REM.ST-REMESSA
      VALUE 'E'
        MOVE 'ENVIADO' TO VSTATUS
      VALUE 'C'
        MOVE 'CONSOLIDADO' TO VSTATUS
      NONE
        MOVE '*' TO VSTATUS
    END-DECIDE
    MOVE INF-REM.DT-REMESSA         TO VDATAALF
    PERFORM SISSD002 VDATANUM
    MOVE EDITED VDATANUM(EM=99/99/9999) TO VDATA
    WRITE(1) 1T VDATA 12T INF-REM.SQ-REMESSA 16T INF-REM.HR-REMESSA
      23T INF-REM.TT-INCLUSAO 31T INF-REM.TT-ALTERACAO
      39T INF-REM.TT-EXCLUSAO 47T INF-REM.TT-INCLUSAO-NOMES
      55T INF-REM.TT-EXCLUSAO-NOMES 64T VSTATUS
  END-READ
  CLOSE PRINTER (1)
END-IF
FETCH 'SCRP4300'
* **********************************
DEFINE SUBROUTINE PEDE-IMPRESSORA
* ********************************
SET CONTROL 'WC07L28FNB12/35'
INPUT WITH TEXT 'INFORME IMPRESSORA.' ALARM
  2/2 'IMPRESSORA:' #IMP (AD=MIT'_')
  4/2 'PF3 - RETORNA.'
IF NOT (*PF-KEY = 'PF3' OR = 'ENTR')
  REINPUT 'TECLA DE FUNCAO INVALIDA' MARK *#IMP ALARM
END-IF
IF #IMP = ' ' AND *PF-KEY = 'ENTR'
  REINPUT 'INFORME IMPRESSORA.' MARK *#IMP ALARM
ELSE
  SET CONTROL 'MB'
  IF *PF-KEY NE 'PF3'
    DEFINE PRINTER (1) OUTPUT WIMPRESSORA
    FORMAT (1) LS=80 PS=60 EJECT OFF
  END-IF
END-IF
SET CONTROL 'WB'
END-SUBROUTINE
* *******************************************
AT TOP OF PAGE (1)
  WRITE (1) NOTITLE
    01T 'COMPANHIA DE PROCESSAMENTO DE DADOS - CODATA'
    72T  *DATE /
    01T 'SSP-IPC - SISTEMA DE IDENTIFICACAO CRIMINAL' 72T
    'PAG:' *PAGE-NUMBER(1) (EM=999) /
    01T 'INFOSEG'
    72T #PROGRAMA# //
    30T 'CONTROLE DE REMESSAS GERADAS' //
    01T 'DATA ENVIO' 12T 'SEQ' 17T 'HORA' 26T 'TR10'  34T 'TR11'
    42T 'TR12' 50T 'TR13' 58T 'TR14' 64T 'SITUACAO' /
    01T '========== ===' 16T '======' 24T '======' 32T '======'
    40T '======'  48T '======' 56T '======' 64T '==============='
END-TOPPAGE
END
