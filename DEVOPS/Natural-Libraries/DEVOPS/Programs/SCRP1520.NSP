* >Natural Source Header 000000
* :Mode S
* :CP
* :LineIncrement 10
* <Natural Source Header
* !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *
* SISTEMA    : Sistema de Identificacao Criminal             *
* PROGRAMA   : SCRPSEN2                                      *
* O QUE FAZ  ? Manutencao de Usuarios                        *
* Autor      : Jose Antonio Coelho                           *
* Data       : 20 de Agosto de 1997                          *
* !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *
* !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *
DEFINE DATA
GLOBAL USING SCRG001
LOCAL USING SCRLSENH
LOCAL
1 #NOME-CADASTRADO      (A30)
1 #MATRICULA-CADASTRADO (N07)
1 #SENHA-CADASTRADO     (A06)
1 #SEU-NIVEL-TXT        (A22)
1 #SEU-NIVEL            (A02)
1 #NOME-CADASTROU       (A30)
1 #MATRICULA-CADASTROU  (N07)
1 #FUNC                 (A09) INIT <'ALTERACAO'> 
1 #OPCAO                (N01)
1 #NOME-DO-MES          (A09)
1 #HORARIO              (A08) INIT <*TIMX>
1 #SENHA-ARQ            (A06)
1 #HORA                 (A08)
1 REDEFINE #HORA
  2 #HH   (N02)
  2 FILLER 01X
  2 #MM   (N02)
  2 FILLER 01X
  2 #SS   (N02)
1 #HORAS                (N06)
1 REDEFINE #HORAS
  2 #HHH                (N02)
  2 #MMM                (N02)
  2 #SSS                (N02)
1 #DATA                 (N08)
1 REDEFINE #DATA
  2 #ANO       (N04)
  2 #MESX      (N02)
  2 #DIA       (N02)
1 #CONF        (A01)
1 #IMP         (A08)
1 #TIPO        (N01) INIT <2>
1 #VAR         (C) INIT <(AD=IP)>
1 #VAR1        (C)
1 #VAR2        (C)
1 #VAR3        (C) INIT <(AD=PN)>
1 #TAB-MES     (A09/1:12)
  INIT <'Janeiro','Fevereiro','Marco','Abril','Maio','Junho',
  'Julho','Agosto','Setembro','Outubro','Novembro','Dezembro'>
END-DEFINE
*
SET  KEY ALL
SET  CONTROL 'MB'
#VLT# := 6
MOVE *PROGRAM TO #PROGRAMA#
INPUT USING MAP 'SCRM1400'
FD0. FIND (1) SENHA-VIEW WITH SEN-MATRICULA = #MATRICULA-CADASTRADO
*                           OR SEN-NOME      = #NOME-CADASTRADO
     MOVE SEN-MAT-ATUALIZADOR TO #MATRICULA-CADASTROU
     MOVE SEN-MATRICULA       TO #MATRICULA-CADASTRADO
     MOVE SEN-NOME            TO #NOME-CADASTRADO
     MOVE SEN-SENHA           TO #SENHA-ARQ
     MOVE SEN-NIVEL           TO #SEU-NIVEL
     MOVE SEN-NIVEL           TO #OPCAO
     IF SEN-DATA-PROC > 0
        MOVE SEN-DATA-PROC    TO #DATA
        MOVE #TAB-MES(#MESX)  TO #NOME-DO-MES
     END-IF
     MOVE SEN-HORA-PROC  TO #HORAS
     MOVE 'Seu nivel de acesso e"' TO #SEU-NIVEL-TXT
     MOVE (AD=IP)  TO #VAR2
     MOVE (AD=I)   TO #VAR
     MOVE (AD=IN)  TO #VAR3
    FD1. FIND (1) SENHA-VIEW WITH SEN-MATRICULA = #MATRICULA-CADASTROU
         IF #MATRICULA-CADASTROU = 9090909
            MOVE 'INICIALIZACAO DO SISTEMA' TO #NOME-CADASTROU
         ELSE
            MOVE SEN-NOME       TO #NOME-CADASTROU
         END-IF
    END-FIND
    INPUT USING MAP 'SCRM1400'
    IF #SENHA-CADASTRADO = ' '
       #SENHA-CADASTRADO := #SENHA-ARQ
    END-IF
    IF #MATRICULA-CADASTRADO = 0
       REINPUT
       'Campo de preenchimento obrigatorio' MARK *#MATRICULA-CADASTRADO
    END-IF
    IF SEN-MATRICULA = #MATRICULA-CADASTRADO
       AND SEN-NOME  = #NOME-CADASTRADO
       AND SEN-SENHA = #SENHA-ARQ
       AND SEN-NIVEL = #OPCAO
       INPUT NO ERASE ALARM 18/20
             'NENHUM CAMPO FOI ALTERADO  - TECLE <ENTER>' (I)
       FETCH 'SCRP1520'
    END-IF
    MOVE *TIMX    TO #HORA
    MOVE #HH      TO #HHH
    MOVE #MM      TO #MMM
    MOVE #SS      TO #SSS
    MOVE *DATN    TO #DATA
    SENHA-VIEW.SEN-MATRICULA       := #MATRICULA-CADASTRADO
    SENHA-VIEW.SEN-NOME            := #NOME-CADASTRADO
    SENHA-VIEW.SEN-SENHA           := #SENHA-CADASTRADO
    SENHA-VIEW.SEN-NIVEL           := #OPCAO
    SENHA-VIEW.SEN-MAT-ATUALIZADOR := #MATRICULA#
    SENHA-VIEW.SEN-DATA-PROC       := #DATA
    SENHA-VIEW.SEN-HORA-PROC       := #HORAS
    UPDATE(FD0.)
    INPUT NO ERASE ALARM 18/29
      'Confirmacao..(S/N).:'(I) #CONF(AD=AIL'_')
    IF #CONF = ' '
      REINPUT 'Confirme "S" ou "N"' ALARM
    END-IF
    IF NOT (#CONF = 'S' OR = 'N')
      REINPUT 'Confirmacao invalida' MARK *#CONF ALARM
    END-IF
    IF #CONF = 'N'
      BACKOUT TRANSACTION
      FETCH 'SCRP1520'
    ELSE
      END TRANSACTION
      INPUT NO ERASE ALARM 19/18
        'ALTERACAO EFETUADA COM SUCESSO - TECLE <ENTER>'(I)
      FETCH 'SCRP1520'
    END-IF
  END-FIND
  IF *COUNTER(FD0.) EQ 0
      REINPUT 'Usuario Nao Cadastrado no Sistema' ALARM
  END-IF
INCLUDE SCRCERRO
END
