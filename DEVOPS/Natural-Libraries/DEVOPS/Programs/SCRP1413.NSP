* >Natural Source Header 000000
* :Mode S
* :CP
* :LineIncrement 10
* <Natural Source Header
* !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *
* SISTEMA    : Sistema de Identificacao Criminal             *
* PROGRAMA   : SCRP1413                                      *
* AUTOR      : MARIA GORETE M S CRUZ                         *
* DATA       : OUTUBRO 1999                                  *
* !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *
* !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *
DEFINE DATA
GLOBAL USING SCRG001
LOCAL USING SCRLCRIM
LOCAL USING SCRLSENH
LOCAL USING TABLELE
LOCAL
1 #IMPRIMIR   (A01)
1 #IMP        (A08)
1 #DATA-ALT-STATUS  (N08)
1 REDEFINE #DATA-ALT-STATUS
  2 #ANO-ALT-STATUS (N04)
  2 #MES-ALT-STATUS (N02)
  2 #DIA-ALT-STATUS (N02)
1 #VARIMP     (C)  INIT<(AD=I)>
1 #OCORRENCIA (N3,0)INIT<1>
1 #OBSERVACOES (A77/1:4)
1 REDEFINE #OBSERVACOES
  2 LEI-ART
    3 #OBSERVACOES-LEI(A30)
    3 #OBSERVACOES-ART(A30)
    3 #OBSERVACOES-FIL(A17)
  2 #OBSERVACOES-FI1(A77)
  2 #OBSERVACOES-FI2(A77)
  2 #OBSERVACOES-FI3(A77)
** CC 1 #OBSERVACOEZ (A77/1:80)
1 #OBSERVACOEZ (A80/1:191)
*
1 #NOME-VITIMA (A60/1:4)
1 REDEFINE #NOME-VITIMA
  2 VIT1
    3 #VITIMA1        (A59)
    3 #SEXO-VITIMA1   (A01)
  2 VIT2
    3 #VITIMA2        (A59)
    3 #SEXO-VITIMA2   (A01)
  2 VIT3
    3 #VITIMA3        (A59)
    3 #SEXO-VITIMA3   (A01)
  2 VIT4
    3 #VITIMA4        (A59)
    3 #SEXO-VITIMA4   (A01)
** CC 1 #VITIMAZ (A60/1:80)
1 #VITIMAZ (A60/1:191)
1 #I(N4)
1 #J(N4)
1 #L(N4)
1 #M(N4)
1 #X(N4)
1 #Y(N4)
1 #W(N4)
* ------------------------------------
1 DESCEL         (A29)
1 #DESC-SEXO     (A01)
1 #DATA-INI       (N08)
1 REDEFINE #DATA-INI
  2 #ANO-INI      (N04)
  2 #MES-INI      (N02)
  2 #DIA-INI      (N02)
1 #DATA-FIM       (N08)
1 REDEFINE #DATA-FIM
  2 #ANO-FIM      (N04)
  2 #MES-FIM      (N02)
  2 #DIA-FIM      (N02)
1 #DATA-INI-P     (N08)
1 REDEFINE #DATA-INI-P
  2 #ANO-INI-P    (N04)
  2 #MES-INI-P    (N02)
  2 #DIA-INI-P    (N02)
1 #REG_GERAL     (N11)
1 #NOME-COMPLETO (A60)
1 #MAE           (A60)
1 #DATA_NASC     (N08)
1 REDEFINE  #DATA_NASC
  2 ANAS (N04)
  2 MNAS (N02)
  2 DNAS (N02)
**1 DEDOS (A07/1:5,1:2)
1 #FALECIDO  (A01)
1 #DELEGACIA (A30)
1 #DESC-MUNICIPIO (A30)
1 #MUNICIPIO(N04)
1 #UF-REG-GERAL (A02)
1 #DATA-REGISTRO (N08)
1 REDEFINE  #DATA-REGISTRO
  2 #ANO-REG (N04)
  2 #MES-REG (N02)
  2 #DIA-REG (N02)
1 #NO-AUTOS (N11)
1 #DATA-ATUACAO (N08)
1 REDEFINE  #DATA-ATUACAO
  2 #ANO-ATUACAO (N04)
  2 #MES-ATUACAO (N02)
  2 #DIA-ATUACAO (N02)
1 #IDENTIDADE (N11)
1 #UF-CI      (A02)
1 #PAI (A60)
1 #MAE-LIMPA (A60)
1 #ALCUNHA (A30)
1 #NOME-ANTERIOR (A60)
1 #SEXO-VIEW     (N01)
1 #SEXO          (A01)
1 #COD-MUN-NASCIMENTO (N04)
1 #UF-NASCIMENTO (A02)
1 #DOC-APRESENTADO (A30)
1 #NUM-DOC-APRESENTADO (N11)
1 #ORGAO-EXP-UF (A15)
1 #PROFISSAO (A30)
1 #INSTRUCAO (N01)
1 #DESC-INSTRUCAO (A20)
1 #NATURALIDADE (A35)
1 #AV-RUA (A30)
1 #NUMERO (N08)
1 #BAIRRO (A20)
1 #MUN-ENDERECO (N04)
1 #UF-ENDERECO (A02)
1 #CEP (N08)
1 #COMPLEMENTO (A20)
1 #STATUS (A01)
1 #DESCREG(A45)
* -------------------------
1 #ALTURA (N01)
1 #DESC-ALTURA  (A20)
1 #ESTADO-CIVIL (N02)
1 #DESC-CIVIL     (A20)
1 #CUTIS          (N01)
1 #DESC-CUTIS     (A20)
1 #COR-CABELO     (N02)
1 #DESC-COR       (A20)
1 #TIPO-CABELO    (N01)
1 #DESC-TIPOCABELO(A20)
1 #COR-OLHOS      (N02)
1 #DESC-OLHO      (A20)
1 #SINAIS         (A60)
1 #COMPLEICAO     (N01)
1 #DESC-COMPLEICAO(A20)
1 #MOTIVO         (A60)
* -------------------------
1 #MATRICULA (N07)
1 #NOME-DIGITADOR (A30)
1 #DATA-DIG (N08)
1 REDEFINE #DATA-DIG
  2 #ANO-DIG (N04)
  2 #MES-DIG (N02)
  2 #DIA-DIG (N02)
1 #HORARIO   (N08)
1 #FICHA1    (N11)
1 #FICHA2    (N11)
1 #VAR       (A01)
1 CHAVE      (A01) INIT <'C'>
1 #MENSAGEM  (A70)
1 #CV-REG-GER  (C)
1 #FUNCAO    (A10) INIT <'Consulta'>
1 #FUNC      (A10) INIT <'ERROS'>
1 #CV-D         (C) INIT <(AD=IP)>
1 #CV-M         (C) INIT <(AD=IP)>
1 #CV-A         (C) INIT <(AD=IP)>
END-DEFINE
* ----------------------------------
SET KEY ALL
SET CONTROL 'MB'
#PROGRAMA# := *PROGRAM
 INPUT #REG_GERAL #DATA-INI-P #DATA-FIM #STATUS
FD33. FIND CRIMINAL-VIEW
    WITH REGISTRO-ESTADUAL-GERAL  = #REG_GERAL  /* EH O REG CRIMINAL
*                                                                      *
* ------------------------- Recupera os Dados ------------------------ *
*                                                                      *
  #REG_GERAL     := CRIMINAL-VIEW.REGISTRO-ESTADUAL-GERAL
  #NOME-COMPLETO := CRIMINAL-VIEW.NOME-COMPLETO
  #MAE           := CRIMINAL-VIEW.NOME-MAE
  #DATA_NASC     := CRIMINAL-VIEW.DATA-NASC
  #FALECIDO      := CRIMINAL-VIEW.FALECIDO
  #IDENTIDADE    := CRIMINAL-VIEW.REGISTRO-FEDERAL-CI
  #UF-CI         := CRIMINAL-VIEW.UF-CI
  #UF-REG-GERAL  := CRIMINAL-VIEW.UF-REG-ESTADUAL-GERAL
  #PAI           := CRIMINAL-VIEW.NOME-PAI
  #DELEGACIA     := CRIMINAL-VIEW.NOME-DELEGACIA
  #DESC-MUNICIPIO:= CRIMINAL-VIEW.MUNICIPIO-REG-GERAL
  #MUNICIPIO     := CRIMINAL-VIEW.COD-MUNIC-REG-GERAL
  #NOME-ANTERIOR := CRIMINAL-VIEW.NOME-ANTERIOR
  #ALCUNHA       := CRIMINAL-VIEW.APELIDO-ALCUNHAS
  #SEXO-VIEW     := CRIMINAL-VIEW.SEXO
  #NATURALIDADE  := CRIMINAL-VIEW.NATURALIDADE
  #COD-MUN-NASCIMENTO :=  CRIMINAL-VIEW.COD-MUNICIPIO-NASCIMENTO
  #UF-NASCIMENTO :=   CRIMINAL-VIEW.UF-NATURALIDADE
  #DOC-APRESENTADO :=        CRIMINAL-VIEW.DOCUM-APRESENTADO
  #NUM-DOC-APRESENTADO :=         CRIMINAL-VIEW.NUME-DOCUM-APRESENT
  #ORGAO-EXP-UF :=     CRIMINAL-VIEW.ORGAO-EXP-UF
  #STATUS :=      CRIMINAL-VIEW.STATUS-REG
  #DATA-ALT-STATUS :=      CRIMINAL-VIEW.DATA-ALTERACAO-STATUS
  #PROFISSAO :=      CRIMINAL-VIEW.PROFISSAO
  #INSTRUCAO :=      CRIMINAL-VIEW.GRAU-INSTRUCAO
  #AV-RUA :=       CRIMINAL-VIEW.AV-RUA
  #NUMERO :=          CRIMINAL-VIEW.NUMERO
  #BAIRRO :=        CRIMINAL-VIEW.BAIRRO
  #MUN-ENDERECO :=     CRIMINAL-VIEW.MUNICIPIO
  #UF-ENDERECO :=      CRIMINAL-VIEW.ESTADO
  #CEP :=      CRIMINAL-VIEW.CEP
  #COMPLEMENTO :=     CRIMINAL-VIEW.COMPLEMENTO
  #DATA-REGISTRO := CRIMINAL-VIEW.DATA-REG
  #DATA-ATUACAO  := CRIMINAL-VIEW.DATA-ATUACAO
  #ALTURA :=         CRIMINAL-VIEW.ALTURA
  #ESTADO-CIVIL :=         CRIMINAL-VIEW.ESTATO-CIVIL
  #CUTIS :=        CRIMINAL-VIEW.CUTIS
  #COR-CABELO :=          CRIMINAL-VIEW.COR-CABELOS
  #TIPO-CABELO :=          CRIMINAL-VIEW.TIPO-CABELOS
  #COR-OLHOS :=        CRIMINAL-VIEW.COR-OLHOS
  #SINAIS :=         CRIMINAL-VIEW.MARCA-PART-CICAT-TATUAG
  #COMPLEICAO :=        CRIMINAL-VIEW.COMPLEICAO
* #MOTIVO :=      CRIMINAL-VIEW.MOTIVO-PRISAO-LEI
  #OCORRENCIA := 1
  MOVE CRIMINAL-VIEW.NOME-VITIMA(*) TO #VITIMAZ(*)
  MOVE CRIMINAL-VIEW.OBS(*) TO #OBSERVACOEZ(*)
  #OBSERVACOES(1) :=  #OBSERVACOEZ(1)
  PERFORM DESC-LEI
  #OBSERVACOES(2) :=  #OBSERVACOEZ(2)
  #OBSERVACOES(3) :=  #OBSERVACOEZ(3)
  #OBSERVACOES(4) :=  #OBSERVACOEZ(4)
  #NOME-VITIMA(1) :=  #VITIMAZ(1)
  #DESC-SEXO := #SEXO-VITIMA1
  PERFORM DESC-SEXO
  #SEXO-VITIMA1 := #DESC-SEXO
  #NOME-VITIMA(2) :=  #VITIMAZ(2)
  #DESC-SEXO := #SEXO-VITIMA2
  PERFORM DESC-SEXO
  #SEXO-VITIMA2 := #DESC-SEXO
  #NOME-VITIMA(3) :=  #VITIMAZ(3)
  #DESC-SEXO := #SEXO-VITIMA3
  PERFORM DESC-SEXO
  #SEXO-VITIMA3 := #DESC-SEXO
  #NOME-VITIMA(4) :=  #VITIMAZ(4)
  #DESC-SEXO := #SEXO-VITIMA4
  PERFORM DESC-SEXO
  #SEXO-VITIMA4 := #DESC-SEXO
  #DATA-DIG         := CRIMINAL-VIEW.DATDOC-ATU
  #MATRICULA        := CRIMINAL-VIEW.MATFUNC-ATU
  #HORARIO          := CRIMINAL-VIEW.HORA-ATU
  MOVE #DATA-DIG TO #DATA-INI
  FD2. FIND (1) SENHA-VIEW WITH SEN-MATRICULA = #MATRICULA
    MOVE SEN-NOME TO #NOME-DIGITADOR
  END-FIND
  DECIDE ON FIRST VALUE OF #STATUS
    VALUE '1'
      #DESCREG := 'REGISTRO ATIVO'
    VALUE '2'
      #DESCREG := 'REGISTRO EM BAIXA'
    VALUE '3'
      #DESCREG := 'REGISTRO CANCELADO'
    VALUE '4'
      #DESCREG := 'LIVRAMENTO CONDICIONAL'
    VALUE '6'
      #DESCREG := 'EXTINCAO PUNIBILIDADE'
    VALUE '7'
      #DESCREG := 'MANDADOS DE PRISAO'
    NONE
      IGNORE
  END-DECIDE
  DECIDE ON FIRST VALUE OF #INSTRUCAO
    VALUE  1
      #DESC-INSTRUCAO := 'Nao Alfabetizado'
    VALUE  2
      #DESC-INSTRUCAO := '1o. Grau Incompleto'
    VALUE  3
      #DESC-INSTRUCAO := '1o. Grau Completo'
    VALUE  4
      #DESC-INSTRUCAO := '2o. Grau Incompleto'
    VALUE  5
      #DESC-INSTRUCAO := '2o. Grau Completo'
    VALUE  6
      #DESC-INSTRUCAO := 'Superior Incompleto'
    VALUE  7
      #DESC-INSTRUCAO := 'Superior Completo'
    NONE
      IGNORE
  END-DECIDE
  DECIDE ON FIRST VALUE OF #SEXO-VIEW
    VALUE 1
      #SEXO := 'M'
    VALUE 2
      #SEXO := 'F'
    VALUE 3
      #SEXO := 'S'
    NONE
      IGNORE
  END-DECIDE
  DECIDE ON FIRST VALUE OF #ALTURA
    VALUE  0
      #DESC-ALTURA    := 'Nao Informada'
    VALUE  1
      #DESC-ALTURA    := '1,60 ou Menos'
    VALUE  2
      #DESC-ALTURA    := '1,61 - 1,70'
    VALUE  3
      #DESC-ALTURA    := '1,71 - 1,80'
    VALUE  4
      #DESC-ALTURA    := '1,81 - 1,90'
    VALUE  5
      #DESC-ALTURA    := '1,91 ou Mais'
    NONE
      IGNORE
  END-DECIDE
*
  TAB := 202
  COD := #ESTADO-CIVIL
  PERFORM SISST001 PESQ RESP
  DESCEL := RESP
  MOVE DESCEL TO #DESC-CIVIL
*
  TAB := 203
  COD := #CUTIS
  PERFORM SISST001 PESQ RESP
  DESCEL := RESP
  MOVE DESCEL TO #DESC-CUTIS
*
  TAB := 205
  COD := #COR-CABELO
  PERFORM SISST001 PESQ RESP
  DESCEL := RESP
  MOVE DESCEL TO #DESC-COR
*
  DECIDE ON FIRST VALUE OF #TIPO-CABELO
    VALUE  0
      #DESC-TIPOCABELO := 'Nao Informado'
    VALUE  1
      #DESC-TIPOCABELO := 'Liso'
    VALUE  2
      #DESC-TIPOCABELO := 'Encaracolado'
    VALUE  3
      #DESC-TIPOCABELO := 'Ondulado'
    VALUE  4
      #DESC-TIPOCABELO := 'Carapinha'
    NONE VALUE
      IGNORE
  END-DECIDE
  TAB := 204
  COD := #COR-OLHOS
  PERFORM SISST001 PESQ RESP
  DESCEL := RESP
  MOVE DESCEL TO #DESC-OLHO
  DECIDE ON FIRST VALUE OF #COMPLEICAO
    VALUE  0
      #DESC-COMPLEICAO := 'Nao Informada'
    VALUE  1
      #DESC-COMPLEICAO := 'Magro'
    VALUE  2
      #DESC-COMPLEICAO := 'Medio'
    VALUE  3
      #DESC-COMPLEICAO := 'Gordo'
    VALUE  4
      #DESC-COMPLEICAO := 'Troncudo'
    VALUE  5
      #DESC-COMPLEICAO := 'Raquitico'
    NONE VALUE
      IGNORE
  END-DECIDE
* SET KEY PF3='SCRP1420'
  IF #NIVEL# LT 4
    MOVE (AD=P) TO #VARIMP
  END-IF
  INPUT USING MAP  'SCRM1421'
  IF *PF-KEY = 'PF3'
     FETCH 'SCRP1412' #DATA-INI-P #DATA-FIM #STATUS
  END-IF
  IF #IMPRIMIR = 'S'
    FETCH 'SCRP1640' #REG_GERAL #REG_GERAL
  END-IF
  INPUT USING MAP  'SCRM1422'
  IF *PF-KEY = 'PF3'
     FETCH 'SCRP1412' #DATA-INI-P #DATA-FIM #STATUS
  END-IF
  INPUT USING MAP  'SCRM1423'
  IF *PF-KEY = 'PF3'
     FETCH 'SCRP1412' #DATA-INI-P #DATA-FIM #STATUS
  END-IF
  PERFORM MOSTRA-OCORRENCIAS    /* GORETE
   AT END OF DATA
     FETCH 'SCRP1412' #DATA-INI-P #DATA-FIM #STATUS
   END-ENDDATA
END-FIND
IF *COUNTER(FD33.) = 0
  MOVE
    '                   Nenhuma Ficha Criminal Foi Encontrada'
    TO #MENSAGEM
  INPUT USING MAP 'SCRMERRO'
     FETCH 'SCRP1412' #DATA-INI-P #DATA-FIM #STATUS
END-IF
DEFINE SUBROUTINE MOSTRA-OCORRENCIAS
#I := 5
IF #OBSERVACOES(1) NE ' '
  IF NOT (*PF-KEY = 'PF3' OR = 'ENTR')
    REINPUT 'Tecla invalida' ALARM
  END-IF
END-IF
* FOR #OCORRENCIA = 2 TO 40
FOR #OCORRENCIA = 2 TO 20
  IF #OCORRENCIA = 2
    COMPUTE #J = #OCORRENCIA + 4
    COMPUTE #L = #OCORRENCIA + 5
    COMPUTE #M = #OCORRENCIA + 6
  ELSE
    COMPUTE #I = #M + 1
    COMPUTE #J = #M + 2
    COMPUTE #L = #M + 3
    COMPUTE #M = #M + 4
  END-IF
  #OBSERVACOES(1)  := #OBSERVACOEZ(#I)
  PERFORM DESC-LEI
  #OBSERVACOES(2)  := #OBSERVACOEZ(#J)
  #OBSERVACOES(3)  := #OBSERVACOEZ(#L)
  #OBSERVACOES(4)  := #OBSERVACOEZ(#M)
*     RESET #OBSERVACOES(*)
  #NOME-VITIMA(1)  := #VITIMAZ(#I)
  #DESC-SEXO := #SEXO-VITIMA1
  PERFORM DESC-SEXO
  #SEXO-VITIMA1 := #DESC-SEXO
  #NOME-VITIMA(2)  := #VITIMAZ(#J)
  #DESC-SEXO := #SEXO-VITIMA2
  PERFORM DESC-SEXO
  #SEXO-VITIMA2 := #DESC-SEXO
  #NOME-VITIMA(3)  := #VITIMAZ(#L)
  #DESC-SEXO := #SEXO-VITIMA3
  PERFORM DESC-SEXO
  #SEXO-VITIMA3 := #DESC-SEXO
  #NOME-VITIMA(4)  := #VITIMAZ(#M)
  #DESC-SEXO := #SEXO-VITIMA4
  PERFORM DESC-SEXO
  #SEXO-VITIMA4 := #DESC-SEXO
  INPUT MAP 'SCRM1423'
**    INPUT NO ERASE
**      14/01 'INCIDENCIAS..:' #OCORRENCIA(AD=OIL)
**      15/05 ' ' #OBSERVACOES-LEI (AD=OIL)
**      15/48 ' ' #OBSERVACOES-ART (AD=OIL)
**      16/01 ' ' #OBSERVACOES(2)(AD=OIL)
**      17/01 ' ' #OBSERVACOES(3)(AD=OIL)
**      18/01 ' ' #OBSERVACOES(4)(AD=OIL)
  SET KEY PF3 ENTR
  IF NOT (*PF-KEY = 'PF3' OR = 'ENTR')
    REINPUT 'Tecla invalida' ALARM
  END-IF
  IF *PF-KEY = 'PF3'
     FETCH 'SCRP1412' #DATA-INI-P #DATA-FIM #STATUS
*   FETCH 'SCRP1400'
  END-IF
  IF #OBSERVACOES(1) = ' '
    ESCAPE BOTTOM
  END-IF
END-FOR
END-SUBROUTINE
DEFINE SUBROUTINE DESC-SEXO
DECIDE ON FIRST VALUE OF #DESC-SEXO
  VALUE '1'
    #DESC-SEXO := 'M'
  VALUE '2'
    #DESC-SEXO := 'F'
  VALUE '3'
    #DESC-SEXO := 'S'
  NONE
    IGNORE
END-DECIDE
END-SUBROUTINE
DEFINE SUBROUTINE DESC-LEI
DECIDE ON FIRST VALUE OF #OBSERVACOES-LEI
  VALUE '1'
    MOVE '1 - Contravencoes Penais' TO #OBSERVACOES-LEI
  VALUE '2'
    MOVE '2 - Crimes Contra a Pessoa' TO #OBSERVACOES-LEI
  VALUE '3'
    MOVE '3 - Crimes Contra o Patrimonio' TO #OBSERVACOES-LEI
  VALUE '4'
    MOVE '4 - Crimes Contra a Prop. Imaterial' TO #OBSERVACOES-LEI
  VALUE '5'
    MOVE '5 - Crimes Contra a Org. do Trabalho' TO #OBSERVACOES-LEI
  VALUE '6'
    MOVE '6 - Crimes contra o Sent. Rel. e contra o Resp. aos Mortos'
      TO #OBSERVACOES-LEI
  VALUE '7'
    MOVE '7 - Crimes Contra os Costumes' TO #OBSERVACOES-LEI
  VALUE '8'
    MOVE '8 - Crimes Contra a Familia' TO #OBSERVACOES-LEI
  VALUE '9'
    MOVE '9 - Crimes Contra a Incol. Publica' TO #OBSERVACOES-LEI
  VALUE '10'
    MOVE '10 - Crimes Contra a Paz Publica' TO #OBSERVACOES-LEI
  VALUE '11'
    MOVE '11 - Crimes contra a Fe Publica' TO #OBSERVACOES-LEI
  VALUE '12'
    MOVE '12 - Crimes contra a Adm. Publica' TO #OBSERVACOES-LEI
  VALUE '13'
    MOVE '13 - Outras' TO #OBSERVACOES-LEI
  NONE
    IGNORE
END-DECIDE
END-SUBROUTINE
**************************
END
