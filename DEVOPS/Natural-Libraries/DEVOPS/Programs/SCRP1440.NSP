* >Natural Source Header 000000
* :Mode S
* :CP
* :LineIncrement 10
* <Natural Source Header
* !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *
*  SISTEMA    : Sistema de Identificacao Criminal            *
*  PROGRAMA   : SCRP001                                      *
*  O QUE FAZ  ? Consulta de Digitadores (Quantas fichas)     *
*  Autor      : Jose Antonio Coelho                          *
*  Data       : 29 de Julho de 1997                          *
* !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *
* !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *
DEFINE DATA
GLOBAL USING SCRG001
LOCAL USING SCRLCRIM
LOCAL USING SCRLSENH
LOCAL
* ---------------------
1 #DATA-INI       (N08)
1 REDEFINE #DATA-INI
  2 #ANO-INI      (N04)
  2 #MES-INI      (N02)
  2 #DIA-INI      (N02)
1 #DATA-FIM       (N08)
1 REDEFINE #DATA-FIM
  2 #ANO-FIM      (N04)
  2 #MES-FIM      (N02)
  2 #DIA-FIM      (N02)
1 #MATRIC    (N07)
1 #NOME-DIGITADOR (A30)
1 #DATA-DIG (N08)
1 REDEFINE #DATA-DIG
  2 #ANO-DIG (N04)
  2 #MES-DIG (N02)
  2 #DIA-DIG (N02)
1 #HORARIO   (N08)
1 #MATRICULA (N07/1:12)
1 #DIGITADOR (A30/1:12)
1 #QUANTIDADE(N06/1:12)
1 #QUANT     (N06)
1 #TOTAL     (N06)
1 #INDEX     (N06)
1 #I         (N02)
1 #J         (N02)
1 #QT-ANT    (N06)
1 #MENSAGEM  (A70)
1 #COLOCACAO (A02/1:12)
1 #CONT      (N18) INIT <100>
1 #OPC       (A01/1:4)
1 #REG       (N18)
END-DEFINE
*
FORMAT (0) PS=60 LS=80
SET KEY ALL
SET CONTROL 'MB'
#FUNCAO#   := 'Consulta'
#PROGRAMA# := *PROGRAM
  INPUT #DATA-INI #DATA-FIM
  SET CONTROL 'N'
  INPUT USING MAP 'SCRM1411'
* FD33. FIND CRIMINAL-VIEW WITH DATDOC-ATU = #DATA-INI THRU #DATA-FIM
  FD33. FIND CRIMINAL-VIEW WITH DATA-REG   = #DATA-INI THRU #DATA-FIM
        WHERE STATUS-REG NE 5
        MOVE *NUMBER TO #REG
        IF *COUNTER(FD33.) = #CONT
               SET CONTROL 'N'
               INPUT NO ERASE
               19/27 '** AGUARDE PROCESSAMENTO **'(I)
               20/21 'Quantidade de Fichas no Periodo..'
                                             #REG  (AD=OIL EM=ZZ.Z99) /
               21/21 'Quantidade de Fichas lidas.......'
                                             #CONT (AD=OIL EM=ZZ.Z99) /
               ADD 100 TO #CONT
        END-IF
        END-ALL
        SORT MATFUNC-ATU
**      USING DATDOC-ATU HORA-ATU
        USING DATA-REG
  AT BREAK MATFUNC-ATU
     ADD 1 TO #INDEX
     MOVE #MATRIC TO #MATRICULA(#INDEX)
     FD2. FIND (1) SENHA-VIEW WITH SEN-MATRICULA = #MATRIC
                   MOVE SEN-NOME TO #DIGITADOR(#INDEX)
     END-FIND
     MOVE #QUANT TO #QUANTIDADE(#INDEX)
     ADD  #QUANT TO #TOTAL
     RESET #QUANT
  END-BREAK
  ADD 1 TO #QUANT
  #MATRIC           := CRIMINAL-VIEW.MATFUNC-ATU
AT END OF DATA
*
   MOVE #QUANTIDADE(1) TO #QT-ANT
   MOVE '<=' TO #COLOCACAO(1)
   FOR #I = 2 TO 10
           IF #QUANTIDADE(#I) > #QT-ANT
              RESET #COLOCACAO(*)
              MOVE #QUANTIDADE(#I) TO #QT-ANT
              MOVE '<=' TO #COLOCACAO(#I)
           END-IF
           IF #QUANTIDADE(#I) = #QT-ANT
              MOVE '<=' TO #COLOCACAO(#I)
           END-IF
   END-FOR
*
   INPUT USING MAP 'SCRM1430'
END-ENDDATA
END-SORT
IF *COUNTER(FD33.) = 0
   MOVE
'               Nenhuma Ficha Foi Cadastrada Nesse Periodo.'
   TO #MENSAGEM
   INPUT USING MAP 'SCRMERRO'
END-IF
FETCH 'SCRP1400'
END
