* >Natural Source Header 000000
* :Mode S
* :CP
* :LineIncrement 10
* <Natural Source Header
**********************************************************************
*   PROGRAMA    : SCRPP003                                           *
*   CLIENTE     : SSP -    SEC. DE SEGURANCA PUBLICA       *
*                          INSTITUTO DE IDENTIFICACAO                *
*   ANALISTA    : ANALICE                                            *
*   OBJETIVO    : CONSULTA BASE CRIMINAL E CIVIL                     *
*   ALTERACAO   : 03/09/2003                                         *
**********************************************************************
DEFINE DATA
GLOBAL USING SCRG001
LOCAL  USING SCRLIDC
LOCAL  USING SCRLPESQ
LOCAL  USING SCRTABEL
LOCAL
1 PESSOA (A245/4)
1 REDEFINE PESSOA
 2 OCORRENCIAS (4)
   3  VNOME        (A50)
   3  VPAI         (A30)
   3  VMAE         (A30)
   3  VDATANASC    (A10)
   3  VALCUNHA     (A15)
   3  VNATURAL     (A25)
   3  VIDENT       (N11)
   3  VEND(A66)
   3  REDEFINE VEND
      4  VCARACT      (A39)
      4  VCARAC2      (A27)
   3  VISN         (N08)
* 1  VUF           (A02/1:02)
* 1  VFICHA        (N11/1:02)
* 1  VOPCAO        (A01/1:02)
1  OPCAO         (N1)
1  I             (N2)
1  J             (N2)
1 LETRA (A1)
1 #DESCREG         (A11)
1 #BAIRRO          (A15)
1 #UF-END          (A02)
1 #END             (A66)
1 #DESC            (A39)
1 #NOME-MAE         (A60)
1 REDEFINE #NOME-MAE
 2 #NOME-MAE-RED      (A30)
 2 #NOME-MAE-FILLER   (A30)
1 #NOME-PAI           (A60)
1 REDEFINE #NOME-PAI
 2 #NOME-PAI-RED      (A30)
 2 #NOME-PAI-FILLER   (A30)
1 #NOME               (A60)
1 REDEFINE #NOME
 2 #NOME-RED         (A50)
 2 #NOME-FILLER      (A10)
1 #STATUSREG       (A01)
1 CODMUN           (A25)
1 DESCMUN          (A29)
1 REDEFINE DESCMUN
  2 DESCMUN-RED (A22)
  2 DESCMUN-FIL (A05)
1 #NOMEFIM         (A30) INIT <'ZZZZZZZZ'>
1 #ALTURA (N01)
1 #DESC-ALTURA  (A20)
1 #CUTIS          (N01)
1 #DESC-CUTIS     (A20)
1 #COR-CABELO     (N02)
1 #DESC-COR       (A20)
1 #TIPO-CABELO    (N01)
1 #DESC-TIPOCABELO(A20)
1 #COR-OLHOS      (N02)
1 #DESC-OLHO      (A09)
1 #SINAIS         (A60)
1 #COMPLEICAO     (N01)
1 #DESC-COMPLEICAO(A20)
1 #MENS  (A47)
1 CHAVE  (A30)
1 REDEFINE CHAVE
  2 A-CHAVE (A11)
1 #REGGER (A11)
1 REDEFINE #REGGER
  2  N-#REGGER (N11)
1 COMUN            (A34)
1 #DATANASC        (N08)
1 REDEFINE #DATANASC
  2 #DATANASC-AA (N04)
  2 #DATANASC-MM (N02)
  2 #DATANASC-DD (N02)
1 #DATA-AA( N4)
1 #FIRST (L)
1 #ENDERECO        (A30)
* 1 #COMPLEMENTO     (A20)
* 1 #BAIRRO          (A20)
 1 #UF-NASC         (A02)
1 A-SUBNOME (A30)
1 #VAR (C)
END-DEFINE
* ----------------------------------
FORMAT LS=160
#PROGRAMA# := *PROGRAM
SET KEY ALL
SET CONTROL 'MB'
REPEAT
  RESET OPCAO
  INPUT (IP=OFF SG=OFF AD=MIL'_') MARK 1
    /// 15T '1'(I) ' =>  NOME NO ARQ CRIMINAL'/
        15T '2'(I) ' =>  NUMERO DO RG.'/
        15T '3'(I) ' =>  NOME NO ARQ CIVIL'/
        15T '9'(I) ' =>  SAIR'///
        15T 'OPCAO:' OPCAO                   //
        15T 'PESQUISA:' CHAVE (AD=U)
*
*   MOVE TRUE TO #FIRST
    MOVE FALSE TO #FIRST
    IF OPCAO = 0 OR OPCAO > 3 AND OPCAO NE 9
       REINPUT 'OPCAO INVALIDA' MARK 1
    END-IF
*
    DECIDE ON FIRST VALUE OF OPCAO
      VALUE 1                              /* NOME NO CRIMINAL
        IF CHAVE NE ' '
           MOVE CHAVE TO A-SUBNOME
           PERFORM GERA-FINAL
           PERFORM PROCURA-CRIMINAL
        END-IF
      VALUE 2
        IF CHAVE NE ' '
          PERFORM ZERA
          MOVE A-CHAVE TO #REGGER
          PERFORM PROCURA-CIVIL-RG
        END-IF
      VALUE 3
        IF CHAVE NE ' '
           MOVE CHAVE TO A-SUBNOME
           PERFORM GERA-FINAL
           PERFORM PROCURA-CIVIL
         END-IF
      VALUE 9
        TERMINATE
      NONE
        ESCAPE BOTTOM
 END-DECIDE
 RESET CHAVE
END-REPEAT
**************************
DEFINE SUBROUTINE PROCURA-CRIMINAL
**************************
   I := 0
   READ SIC-CADASTRO-VIEW BY SB-NOME STARTING FROM A-SUBNOME
   THRU #NOMEFIM
      ADD 1 TO I
      MOVE *ISN TO VISN(I)
      #STATUSREG := STATUS-REG
      #CUTIS     := CUTIS
      #ALTURA    := ALTURA
      #COR-OLHOS := COR-OLHOS
      MOVE DATA-NASC TO #DATANASC
      IF NOT(#DATANASC = MASK(YYYYMMDD))
         IF  #DATANASC-AA LT 1890
            MOVE 1900 TO #DATANASC-AA
         END-IF
         IF #DATANASC-MM LT 1
            MOVE 01 TO #DATANASC-MM
         END-IF
         IF #DATANASC-DD LT 1
            MOVE 01 TO #DATANASC-DD
         END-IF
      END-IF
      PERFORM SISSD002 #DATANASC
      MOVE EDITED #DATANASC(EM=99/99/9999) TO VDATANASC(I)
      MOVE NOME-COMPLETO TO #NOME
      MOVE NOME-MAE TO #NOME-MAE
      MOVE NOME-PAI TO #NOME-PAI
      MOVE #NOME-RED TO VNOME(I)
      PERFORM PESQ-DADOS-CRIMINAL
      MOVE #NOME-MAE-RED TO VMAE(I)
      MOVE #NOME-PAI-RED TO VPAI(I)
      MOVE #DESC TO VCARACT(I)
      MOVE COMUN  TO VNATURAL(I)
      MOVE REGISTRO-ESTADUAL-GERAL TO VIDENT(I)
      IF I = 4
        PERFORM MOSTRA-TELA
        RESET I
      END-IF
   END-READ
   IF *COUNTER(0152) =  0
      PERFORM MOSTRA-ERRO
   ELSE
      IF I GT 0 AND I < 4
         PERFORM MOSTRA-TELA
         RESET I
      END-IF
*     WRITE NOTITLE'</BEAN_RESULT>'
   END-IF
END-SUBROUTINE
*******************************
* *******************************
DEFINE SUBROUTINE PROCURA-CIVIL
* *******************************
   I := 0
   READ DID-CADASTRO-VIEW BY SB-NOME STARTING FROM A-SUBNOME
   THRU #NOMEFIM
      ADD 1 TO I
      MOVE *ISN TO VISN(I)
      #ENDERECO        := DIAIDE-ENDERE
*     #COMPLEMENTO     := DIAIDE-COMPLE
      #BAIRRO          := DIAIDE-BAIRRO
      MOVE DINIDE-DATNAS TO #DATANASC
      PERFORM SISSD002 #DATANASC
      MOVE EDITED #DATANASC(EM=99/99/9999) TO VDATANASC(I)
      MOVE  DIAIDE-NOMIDE TO #NOME
      MOVE  DIAIDE-NOMPAI TO #NOME-PAI
      MOVE  DIAIDE-NOMMAE TO #NOME-MAE
      PERFORM PESQ-DADOS-CIVIL
      MOVE #NOME-RED TO VNOME(I)
      MOVE #NOME-MAE-RED TO VMAE(I)
      MOVE #NOME-PAI-RED TO VPAI(I)
      MOVE #END   TO VEND(I)
      MOVE COMUN  TO VNATURAL(I)
      MOVE DINIDE-REGGER TO VIDENT(I)
      IF I = 4
        PERFORM MOSTRA-TELA-CIVIL
        RESET I
      END-IF
   END-READ
   IF *COUNTER(0204) = 0
       PERFORM MOSTRA-ERRO
   ELSE
     IF I GT 0 AND I < 4
        PERFORM MOSTRA-TELA-CIVIL
     END-IF
*    WRITE NOTITLE'</BEAN_RESULT>'
   END-IF
END-SUBROUTINE
* *******************************
DEFINE SUBROUTINE PROCURA-CIVIL-RG
* *******************************
   I := 0
   READ DID-CADASTRO-VIEW BY DINIDE-REGGER STARTING FROM N-#REGGER
   THRU N-#REGGER
      ADD 1 TO I
      MOVE *ISN TO VISN(I)
      #ENDERECO        := DIAIDE-ENDERE
      #BAIRRO          := DIAIDE-BAIRRO
      MOVE  DINIDE-DATNAS TO #DATANASC
      PERFORM SISSD002 #DATANASC
*     DISPLAY #DATANASC DINIDE-DATNAS
      MOVE #DATANASC  TO VDATANASC(I)
      MOVE  DIAIDE-NOMIDE TO #NOME
      MOVE  DIAIDE-NOMPAI TO #NOME-PAI
      MOVE  DIAIDE-NOMMAE TO #NOME-MAE
      MOVE #NOME-RED TO VNOME(I)
      MOVE #NOME-MAE-RED TO VMAE(I)
      MOVE #NOME-PAI-RED TO VPAI(I)
      MOVE #END  TO VEND(I)
      MOVE COMUN  TO VNATURAL(I)
      MOVE DINIDE-REGGER TO VIDENT(I)
      PERFORM PESQ-DADOS-CIVIL
      MOVE #NOME-RED TO VNOME(I)
      MOVE #NOME-MAE-RED TO VMAE(I)
      MOVE #NOME-PAI-RED TO VPAI(I)
      MOVE #END  TO VEND(I)
      MOVE COMUN  TO VNATURAL(I)
      IF I = 4
        PERFORM MOSTRA-TELA-CIVIL
        RESET I
      END-IF
   END-READ
   IF *COUNTER(0242) = 0
      PERFORM MOSTRA-ERRO
   ELSE
      IF I GT 0 AND I < 4
         PERFORM MOSTRA-TELA-CIVIL
      END-IF
*     WRITE NOTITLE'</BEAN_RESULT>'
   END-IF
END-SUBROUTINE
* **************************************
DEFINE SUBROUTINE PESQ-DADOS-CRIMINAL
**
TAB        := 1       /* PESQUISA TABELA DE MUNICIPIOS
COD        := COD-MUNICIPIO-NASCIMENTO
PERFORM SISST001 PESQ RESP
MOVE UF-NATURALIDADE TO #UF-NASC
*
IF RESP = '#'
  TAB        := 37      /* PESQUISA TABELA DE PAISES
  COD        := DINIDE-COMUNA
  PERFORM SISST001 PESQ RESP
  MOVE '##' TO #UF-NASC
END-IF
*
* CODM       := COD
DESCMUN    := RESP
COMPRESS DESCMUN-RED '-' #UF-NASC   INTO COMUN LEAVING NO SPACE
  DECIDE ON FIRST VALUE OF #STATUSREG
    VALUE '1'
      #DESCREG := 'REG ATIVO'
    VALUE '2'
      #DESCREG := 'EM BAIXA'
    VALUE '3'
      #DESCREG := 'CANCELADO'
    VALUE '4'
      #DESCREG := 'CONDICIONAL'
    VALUE '6'
      #DESCREG := 'EXT.PUNIBIL'
    VALUE '7'
      #DESCREG := 'MAND.PRISAO'
    NONE
      IGNORE
  END-DECIDE
  DECIDE ON FIRST VALUE OF #ALTURA
    VALUE  0
      #DESC-ALTURA    := '  '
    VALUE  1
      #DESC-ALTURA    := 'Ate 1,60 '
    VALUE  2
      #DESC-ALTURA    := '1,61-1,70'
    VALUE  3
      #DESC-ALTURA    := '1,71-1,80'
    VALUE  4
      #DESC-ALTURA    := '1,81-1,90'
    VALUE  5
      #DESC-ALTURA    := 'Mais 1,90'
    NONE
      IGNORE
  END-DECIDE
  TAB := 203
  COD := #CUTIS
  PERFORM SISST001 PESQ RESP
  #DESC-CUTIS := RESP
    DECIDE ON FIRST VALUE OF #COR-OLHOS
      VALUE  0
        #DESC-OLHO      := '  '
     VALUE  11
        #DESC-OLHO      := 'Castanhos'
      VALUE  12
       #DESC-OLHO      := 'Pretos'
     VALUE  13
       #DESC-OLHO      := 'Azuis'
     VALUE  14
       #DESC-OLHO      := 'Verdes'
    VALUE  15
       #DESC-OLHO      := '2 Cores'
     NONE VALUE
        IGNORE
    END-DECIDE
  COMPRESS  #DESC-ALTURA  #DESC-CUTIS 'OLHOS'
  #DESC-OLHO INTO #DESC
END-SUBROUTINE
* **
* **************************************
DEFINE SUBROUTINE PESQ-DADOS-CIVIL
**
TAB        := 1       /* PESQUISA TABELA DE MUNICIPIOS
COD        := DINIDE-COMUNA
PERFORM SISST001 PESQ RESP
MOVE DIAIDE-UFNASC     TO #UF-NASC
*
IF RESP = '#'
  TAB        := 37      /* PESQUISA TABELA DE PAISES
  COD        := DINIDE-COMUNA
  PERFORM SISST001 PESQ RESP
  MOVE '##' TO #UF-NASC
END-IF
*
DESCMUN    := RESP
COMPRESS DESCMUN-RED '-' #UF-NASC   INTO COMUN LEAVING NO SPACE
TAB        := 1       /* PESQUISA TABELA DE MUNICIPIOS
COD        := DINIDE-COMUEN
PERFORM SISST001 PESQ RESP
DESCMUN    := RESP
MOVE DIAIDE-UFMURE     TO #UF-END
*
COMPRESS 'END:' DIAIDE-ENDERE '-' DESCMUN '-' #UF-END INTO #END
END-SUBROUTINE
* **
DEFINE SUBROUTINE MOSTRA-ERRO
 #MENS := 'NENHUM REGISTRO ENCONTRADO COM O NOME INFORMADO'
* WRITE NOTITLE'<BEAN_ERROR>' #MENS'</BEAN_ERROR>'
 WRITE  #MENS
END-SUBROUTINE
*
DEFINE SUBROUTINE MOSTRA-TELA
 IF #FIRST
    WRITE NOTITLE'<bean_result>'
    MOVE FALSE TO #FIRST
 END-IF
 INPUT USING MAP 'SCRM9903'
 RESET PESSOA(*)
END-SUBROUTINE
*
DEFINE SUBROUTINE MOSTRA-TELA-CIVIL
IF #FIRST
  WRITE NOTITLE'<bean_result>'
  MOVE FALSE TO #FIRST
END-IF
INPUT USING MAP 'SCRM9904'
RESET PESSOA(*)
END-SUBROUTINE
*
DEFINE SUBROUTINE ZERA
MOVE RIGHT CHAVE TO A-CHAVE
EXAMINE FULL A-CHAVE  FOR ' ' REPLACE '0'
END-SUBROUTINE
*
DEFINE SUBROUTINE GERA-FINAL
RESET #NOMEFIM
FOR J = 30 TO 1 STEP -1
 LETRA:= SUBSTR(A-SUBNOME,J,1)
 IF LETRA NE ' '
   #NOMEFIM := SUBSTR(A-SUBNOME,1,J)
   COMPRESS #NOMEFIM  'Z'INTO #NOMEFIM LEAVING NO SPACE
   MOVE 1 TO J
 END-IF
END-FOR
END-SUBROUTINE
END
