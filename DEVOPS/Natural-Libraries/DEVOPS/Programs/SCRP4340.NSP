* >Natural Source Header 000000
* :Mode S
* :CP
* :LineIncrement 10
* <Natural Source Header
* !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *
* SISTEMA    : Sistema de Identificacao Criminal             *
* PROGRAMA   : SCRP4340 - IMPRIME REJEICOES
* AUTOR      : ANALICE                                       *
* DATA       : MARCO 2004                                    *
* !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *
DEFINE DATA
GLOBAL USING SCRG001
LOCAL USING SCRLSENH
LOCAL USING SCRLINFJ
LOCAL
1 #EMPRESA   (A08) INIT <' CODATA '>
1 #PROGRAM   (A08) INIT  <*PROGRAM>
1 #FUNC      (A10) INIT <'INFOSEG'>
1 VTIPO-REG  (A02/1:7)
1 VNOME      (A70/1:7)
1 VREG-GERAL (A20/1:7)
1 VMAE       (A70/1:7)
1 VPAI       (A70/1:7)
1 VDATANASC  (A10/1:7)
1 VSEXO      (A01/1:7)
1 VCUTIS     (A02/1:7)
1 VNATURAL   (A35/1:7)
1 VNATURALUF (A02/1:7)
1 VERRO      (A70/1:7)
1 WDATA     (A08)
1 REDEFINE WDATA
  2  W-ANO   (A4)
  2  W-MES   (A2)
  2  W-DIA   (A2)
1 WDATAINVN  (N08)
1 REDEFINE WDATAINVN
  2 WDATAINV (A08)
  2 REDEFINE WDATAINV
    3  WI-DIA (A2)
    3  WI-MES (A2)
    3  WI-ANO (A4)
1 I  (N02)
1 XX-ERRO   (A96)
1 REDEFINE XX-ERRO
  2  XX-ERRO1  (A70)
  2  XX-ERRO2A (A26)
1 ESTOURO (N06)
1 ATUAL   (A01)
1 WCOMANDO         (A5)
1 WIMPRESSORA      (A8)
1 REDEFINE WIMPRESSORA
  2 #IMP             (A4)
END-DEFINE
**************************
INCLUDE SISCPFS
*
* REPEAT
SET CONTROL 'MB'
IF *PF-KEY =  'PF3'
  FETCH 'SCRP4300'
END-IF
I := 0
PERFORM PEDE-IMPRESSORA
IF *PF-KEY NE 'PF3'
  READ INF-REJ BY NU-IG-UF
    PERFORM MONTA-REL
    AT END OF DATA
       IF I >= 1
         WRITE(1) NOTITLE USING FORM 'SCRF4340'
        CLOSE PRINTER(1)
      RESET VTIPO-REG(*) VNOME(*) VREG-GERAL(*) VPAI(*) VMAE(*) VSEXO(*)
          VDATANASC(*) VCUTIS(*) VNATURAL(*) VNATURALUF(*) VERRO(*)
      END-IF
    END-ENDDATA
  END-READ
END-IF
FETCH 'SCRP4300'
* ***************************
DEFINE SUBROUTINE MONTA-REL
*******************************
ADD 1 TO I
MOVE INF-REJ.TP-REGISTRO   TO VTIPO-REG(I)
MOVE INF-REJ.TP-REGISTRO   TO VTIPO-REG(I)
MOVE INF-REJ.NU-IG-UF      TO VREG-GERAL(I)
MOVE INF-REJ.NM-NOME       TO VNOME(I)
MOVE INF-REJ.NM-PAI        TO VPAI(I)
MOVE INF-REJ.NM-MAE        TO VMAE(I)
MOVE INF-REJ.DT-NASCIMENTO TO WDATA
MOVE W-ANO TO WI-ANO
MOVE W-MES TO WI-MES
MOVE W-DIA TO WI-DIA
MOVE EDITED WDATAINVN(EM=99/99/9999) TO VDATANASC(I)
MOVE INF-REJ.CD-SEXO            TO VSEXO(I)
MOVE INF-REJ.CD-CUTIS           TO VCUTIS(I)
MOVE INF-REJ.NM-NATURALIDADE    TO VNATURAL(I)
MOVE INF-REJ.SG-UF-NATURALIDADE TO VNATURALUF(I)
MOVE INF-REJ.DS-ERRO            TO XX-ERRO
MOVE XX-ERRO1 TO VERRO(I)
IF I = 7
  WRITE(1) NOTITLE USING FORM 'SCRF4340'
  I:= 0
  RESET VTIPO-REG(*) VNOME(*) VREG-GERAL(*) VPAI(*) VMAE(*) VSEXO(*)
    VDATANASC(*) VCUTIS(*) VNATURAL(*) VNATURALUF(*) VERRO(*)
END-IF
IF *PF-KEY =  'PF3'
  FETCH 'SCRP4300'
END-IF
END-SUBROUTINE
* **********************************
DEFINE SUBROUTINE PEDE-IMPRESSORA
* ********************************
SET CONTROL 'WC07L28FNB12/35'
INPUT WITH TEXT 'INFORME IMPRESSORA.' ALARM
  2/2 'IMPRESSORA:' #IMP (AD=MIT'_')
  4/2 'PF3 - RETORNA.'
IF NOT (*PF-KEY = 'PF3' OR = 'ENTR')
  REINPUT 'TECLA DE FUNCAO INVALIDA' MARK *#IMP ALARM
END-IF
IF #IMP = ' ' AND *PF-KEY = 'ENTR'
  REINPUT 'INFORME IMPRESSORA.' MARK *#IMP ALARM
ELSE
 SET CONTROL 'MB'
  IF *PF-KEY NE 'PF3'
    END TRANSACTION
    COMPRESS 'H' #IMP INTO WCOMANDO LEAVING NO SPACE
    DEFINE PRINTER (1) OUTPUT WIMPRESSORA
    FORMAT (1) LS=80 PS=60 EJECT OFF
  END-IF
END-IF
SET CONTROL 'WB'
END-SUBROUTINE
* *******************************************
END
