* >Natural Source Header 000000
* :Mode S
* :CP
* :LineIncrement 10
* <Natural Source Header
************************************************************************
* PROGRAMA : SCRP3300                                                  *
* OBJETIVO : CONSULTA EMISSOES DE ATESTADOS DE ANTECEDENTES            *
*            POR NOME, PERIODO, FINALIDADE E/OU USUARIO                *
* ANALISTA : MARIA GORETE DE MEDEIROS S CRUZ                           *
* DATA     : JAN/1999                                                  *
*            MIGRAÇÃO - FEV/2011 - CZ                                  *
* -------------------------------------------------------------------- *
* >>> ALTERACOES FEITAS POR VERONICA EM ABRIL/2017                     *
*                                                                      *
*    - Alteracao dos campos e do layout do mapa MAP15V :               *
*      #NUMDOC (N11) --> (A20), p/tratar qualquer formato documento    *
*      #NOMEIDE(A60) --> (A48), apenas definicao nos mapas             *
*      #DESCRICAO(A30) -- Novo campo p/descrever funcao no cabecalho   * 
*      ocorrencias dos arrays de 15 --> 14                             *
*                                                                      *
************************************************************************
DEFINE DATA
GLOBAL USING SCRG001
* LOCAL  USING VWDIDEMI
LOCAL  USING VWEMISSA     /* VERONICA - 04.04.17
LOCAL  USING SISLDAT8
LOCAL  USING SISLCT01
LOCAL
1 #I           (N04)
1 #DESCRICAO-M (A30)  INIT <'EMITIDOS POR NOME'>  /* VERONICA - 04.04.17 
1 #OPC         (A01/1:2)
* 1 #NUMDOC      (A11/1:15)
1 #NUMDOC      (A20/1:14)
1 #UFCONS      (A20/1:14)
1 #POSTO       (A4/1:14)
1 MATRICULA    (A7/1:14)
1 FINALIDADE   (A16/1:14)
1 #FINALIDADE  (N02)
1 #NOME        (A60)
* 1 #NOMEIDE     (A60/1:14)
1 #NOMEIDE     (A48/1:14)      /* VERONICA - 04.04.17
1 #MAE         (A60)
1 MAE          (A60/1:14)
1 DATEMI       (A10/1:14)
1 HOREMI       (A08/1:14)
1 #DATEMI      (N08)
1 #DATAINICIO  (N08)
1 #USUARIO     (N07)
1 #SAIDA       (A01)
1 #ERRO        (L)
1 #IMP         (A08)
1 #PROGRAMA    (A8) INIT <*PROGRAM>
1 DTINICIO     (A8)
1 REDEFINE DTINICIO
  2 DT1        (N8)
1 DTFINAL      (A8)
1 REDEFINE DTFINAL
  2 DT2        (N8)
1 #CONT        (N4)
**---------------------------------------
1 ESTOURO   (N02)
1 #MSGCRIM   (A60)
1 #EXISTE-SEC          (A01)
1 #NUMERO2             (N05)
1 #SEMELHANTE          (A01)
1 #J2                  (N02)
1 #K                   (N02)
1 #SEC                 (A06)
1 #SEC-ANT             (A06)
1 #AUX                 (A06)
1 #CHAVE-COMBINA       (A120)
1 REDEFINE #CHAVE-COMBINA
  2 #VET               (A06/1:20)
1 #FONETO1           (A06/1:61)
1 #FONETO-NOME-DADO        (A06)
1 REDEFINE #FONETO-NOME-DADO
  2 FONETO-NOME-DADO        (B06)
1 #FONETO-MAE-DADO        (A06)
1 REDEFINE #FONETO-MAE-DADO
  2 FONETO-MAE-DADO        (B06)
1 #INFORMACOES-GERAIS  (A97)
1 REDEFINE #INFORMACOES-GERAIS
  2 #NOME-DA-PESSOA           (A80)
  2 #TAMANHO-NOME-PESSOA      (B02)
  2 #TIPO-NOME                (A01)
  2 #NUMERO-COMPONENTES       (B02)
  2 #TIPO-COMBINACAO          (A01)
  2 #NUMERO-COMBINACOES       (B02)
  2 #INICIO-COMBINACAO        (B02)
  2 #CHAVE-NOME-DADO          (A06)
  2 REDEFINE #CHAVE-NOME-DADO
    3 CHAVE-NOME-DADO         (B06)
  2 #FLAG                     (A01)
**1 #FILLER                  (A100)
1 #CHAVE-COMB-2A21            (A240)
1 #CHAVE-COMB-2A22            (A030)
1 #CHAVE-COMB-3A31            (A240)
1 #CHAVE-COMB-3A32            (A240)
1 #CHAVE-COMB-3A33            (A240)
1 #CHAVE-COMB-4A41            (A240)
1 #CHAVE-COMB-4A42            (A240)
1 #CHAVE-COMB-4A43            (A240)
1 #CHAVE-COMB-4A44            (A240)
1 #CHAVE-COMB-4A45            (A240)
1 #CHAVE-COMB-4A46            (A240)
**
1 #CHAVE-FONETICA
  2 #CONSULTA                      (L)
  2 #TEXTO-CONSULTA                (A60)
  2 #TEXTO-FONETICO                (A15/1:10)
1 #CHAVE-FONETICA-MAE
  2 #CONSULTA-MAE                  (L)
  2 #TEXTO-CONSULTA-MAE            (A60)
  2 #TEXTO-FONETICO-MAE            (A15/1:10)
**
1 #FLAG-CRIMINAL                   (N01)
1 #IJ                              (N02)
*
1 #FLAGN(N1) /* HOMONIMO INDIVIDUO
1 #FLAGM(N1) /* HOMONIMO MAE
**
END-DEFINE
*
INCLUDE SISCPFS
SET KEY ALL
FORMAT LS=150
RP0.
REPEAT
  RESET MAE(*) #NOMEIDE(*)  FINALIDADE(*) #POSTO(*)
    #NUMDOC(*) #UFCONS(*) DATEMI(*) HOREMI(*) MATRICULA(*)
    #MAE   #NOME #SAIDA
  SET CONTROL 'WC08L80B09/40F'
  INPUT (SG=OFF IP=OFF) ALARM
    /   02T 'IMPRESSORA/TELA/QUANTIDADE (I/T/Q):' #SAIDA(AD=AIL'_')
    /   02T 'REQUERENTE:' #NOME(AD=AIL'_')
    /   02T 'MAE.......:' #MAE(AD=AIL'_')
    //  02T 'PF3 - RETORNA'
  IF *PF-KEY = 'PF3'
    SET CONTROL 'WB'
    FETCH 'SCRP3000'
  END-IF
  IF NOT(#SAIDA = 'T' OR = 'I' OR = 'Q')
    REINPUT 'Opcao Invalida' MARK *#SAIDA ALARM
  END-IF
  IF (#NOME = ' ')
    REINPUT 'INFORME NOME DO REQUERENTE' MARK *#NOME ALARM
  END-IF
  IF (#MAE = ' ')
    REINPUT 'INFORME NOME DA MAE' MARK *#MAE ALARM
  END-IF
  IF NOT( *PF-KEY = 'PF3' OR = 'ENTR')
    REINPUT 'TECLA DE FUNCAO INVALIDA' ALARM
  ELSE
    SET CONTROL 'WB'
  END-IF
  IF *PF-KEY = 'PF3'
    FETCH 'SCRP3000'
  END-IF
  IF NOT(#SAIDA = 'I' OR = 'T' OR = 'Q')
    REINPUT 'OPCAO INVALIDA' MARK *#SAIDA ALARM
  END-IF
  IF #SAIDA = 'I'
    IF #NOME NE ' '
      PERFORM CHAMA-FONETICA
    END-IF
    PERFORM PEDE-IMPRESSORA
    FETCH 'IMP2V' DATINI DATFIM #USUARIO #IMP #NOME #MAE #FINALIDADE 
      #TEXTO-FONETICO(*) #TEXTO-FONETICO-MAE(*)
      #DESCRICAO-M 
  ELSE
    SET KEY PF1='%W>' PF2='%W<'
    PERFORM DETALHE
  END-IF
END-REPEAT
*
*************************
DEFINE SUBROUTINE DETALHE
*************************
RESET #I #CONT
**---- PESQUISA POR NOME------------**
IF #NOME NE ' '
  PERFORM CHAMA-FONETICA
  RESET ESTOURO
*
  FIND DID-EMISSAO-VIEW WITH COD-FONETICO-SECUNDARIO
      EQ #TEXTO-FONETICO(01)
      AND COD-FONETICO-SECUNDARIO     EQ #TEXTO-FONETICO(02)
      AND COD-FONETICO-SECUNDARIO     EQ #TEXTO-FONETICO(03)
      AND COD-FONETICO-SECUNDARIO     EQ #TEXTO-FONETICO(04)
      AND COD-FONETICO-SECUNDARIO     EQ #TEXTO-FONETICO(05)
      AND COD-FONETICO-SECUNDARIO     EQ #TEXTO-FONETICO(06)
      AND COD-FONETICO-SECUNDARIO     EQ #TEXTO-FONETICO(07)
      AND COD-FONETICO-SECUNDARIO     EQ #TEXTO-FONETICO(08)
      AND COD-FONETICO-SECUNDARIO     EQ #TEXTO-FONETICO(09)
      AND COD-FONETICO-SECUNDARIO     EQ #TEXTO-FONETICO(10)
      AND COD-FONETICO-SECUNDARIO-MAE EQ #TEXTO-FONETICO-MAE(01)
      AND COD-FONETICO-SECUNDARIO-MAE EQ #TEXTO-FONETICO-MAE(02)
      AND COD-FONETICO-SECUNDARIO-MAE EQ #TEXTO-FONETICO-MAE(03)
      AND COD-FONETICO-SECUNDARIO-MAE EQ #TEXTO-FONETICO-MAE(04)
      AND COD-FONETICO-SECUNDARIO-MAE EQ #TEXTO-FONETICO-MAE(05)
      AND COD-FONETICO-SECUNDARIO-MAE EQ #TEXTO-FONETICO-MAE(06)
      AND COD-FONETICO-SECUNDARIO-MAE EQ #TEXTO-FONETICO-MAE(07)
      AND COD-FONETICO-SECUNDARIO-MAE EQ #TEXTO-FONETICO-MAE(08)
      AND COD-FONETICO-SECUNDARIO-MAE EQ #TEXTO-FONETICO-MAE(09)
      AND COD-FONETICO-SECUNDARIO-MAE EQ #TEXTO-FONETICO-MAE(10)
*
* ------ VERIFICANDO SE O NOME DO INDIVIDUO E O NOME DA MÃE SÃO
* ------ HOMÔNIMOS.
*
* ------ INDIVIDUO
*
    RESET #FLAGN
    FOR #IJ = 1 TO 10
      IF COD-FONETICO-SECUNDARIO(#IJ) EQ ' '
        ESCAPE BOTTOM
      END-IF
      IF COD-FONETICO-SECUNDARIO(#IJ) NE
          #TEXTO-FONETICO(#IJ)
        ASSIGN #FLAGN = 1
        ESCAPE BOTTOM
      END-IF
    END-FOR
* ------
*
* ------ MAE
    RESET #FLAGM
    FOR #IJ = 1 TO 10
      IF COD-FONETICO-SECUNDARIO-MAE(#IJ) EQ ' '
        ESCAPE BOTTOM
      END-IF
      IF COD-FONETICO-SECUNDARIO-MAE(#IJ) NE
          #TEXTO-FONETICO-MAE(#IJ)
        ASSIGN #FLAGM = 1
        ESCAPE BOTTOM
      END-IF
    END-FOR
*
* -- VERIFICA SE A MAE E O INDIVIDUO SÃO HOMONIMOS
*
    IF #FLAGM EQ 1 OR #FLAGN EQ 1
      ESCAPE TOP
    END-IF
**
    AT END OF DATA
      IF #I > 0 AND #SAIDA = 'T'
        SET CONTROL 'WB'
        INPUT USING MAP 'MAP15V'
        IF *PF-KEY = 'PF3'
          SET CONTROL 'WB'
        END-IF
      END-IF
    END-ENDDATA
    ADD 1 TO #I
    ADD 1 TO #CONT
    ADD 1 TO ESTOURO
    IF ESTOURO = 50
      SET CONTROL 'N'
      INPUT NO ERASE WITH TEXT 'AGUARDE...PROCESSANDO' 05/01 ' '
      RESET ESTOURO
    END-IF
    IF #SAIDA = 'T'
      PERFORM DADOS
      IF #I > 13
        SET CONTROL 'WB'
        INPUT USING MAP 'MAP15V'
        RESET #I FINALIDADE(*) DATEMI(*) #NUMDOC(*) #POSTO(*)
          HOREMI(*) MATRICULA(*) #UFCONS(*) #NOMEIDE(*) MAE(*)
        IF *PF-KEY = 'PF3'
          SET CONTROL 'WB'
        END-IF
      END-IF
    END-IF
  END-FIND
  IF #CONT = 0
    REINPUT 'NENHUM ATESTADO ENCONTRADO' ALARM
  ELSE
    IF #SAIDA = 'Q'
      SET CONTROL 'WC05L35B10/40F'
      INPUT (SG=OFF IP=OFF) ALARM
        /   02T 'QUANTIDADE ATESTADOS: ' #CONT(AD=OIL)
      IF NOT(*PF-KEY = 'PF3' OR = 'ENTR')
        REINPUT 'TECLA DE FUNCAO INVALIDA'
      ELSE
        SET CONTROL 'WB'
      END-IF
      IF *PF-KEY = 'PF3'
        FETCH 'SCRP3300'
      END-IF
    END-IF
  END-IF
END-IF
END-SUBROUTINE
*********************************
DEFINE SUBROUTINE PEDE-IMPRESSORA
*********************************
SET CONTROL 'WC07L28FNB12/35'
INPUT WITH TEXT 'INFORME IMPRESSORA.' ALARM
  2/2 'IMPRESSORA:' #IMP (AD=MIT'_')
  4/2 'PF3 - RETORNA.'
IF #IMP = ' ' AND *PF-KEY = 'ENTR'
  REINPUT 'Informe impressora.' MARK *#IMP ALARM
END-IF
IF NOT (*PF-KEY = 'PF3' OR = 'ENTR')
  REINPUT 'Tecla de funcao invalida' MARK *#IMP ALARM
ELSE
  SET CONTROL 'WB'
END-IF
IF *PF-KEY = 'PF3'
  FETCH 'SCRP3300'
END-IF
SET CONTROL 'WB'
DEFINE PRINTER (1) OUTPUT 'LPT1'
EJECT OFF (1)
END-SUBROUTINE
***********************
DEFINE SUBROUTINE DADOS
***********************
DECIDE ON FIRST VALUE OF DIDEMI-FINALIDADE
  VALUE 1
    MOVE 'FINS DE TRABALHO' TO FINALIDADE(#I)
  VALUE 2
    MOVE 'COMPRA DE ARMA'   TO FINALIDADE(#I)
  VALUE 3
    MOVE 'REG. DE ARMA'     TO FINALIDADE(#I)
  VALUE 4
    MOVE 'PORTE DE ARMA'    TO FINALIDADE(#I)
  VALUE 5
    MOVE 'FINS CIVIS'      TO FINALIDADE(#I)
  VALUE 6
    MOVE 'OFICIO'      TO FINALIDADE(#I)
  VALUE 7
    MOVE 'FAX'      TO FINALIDADE(#I)
  VALUE 8
    MOVE 'CERT. CRIMINAIS'      TO FINALIDADE(#I)
  VALUE 9
    MOVE 'CANC. DE NOTA' TO FINALIDADE(#I)
  VALUE 10
    MOVE 'CART. FUNCIONAL' TO FINALIDADE(#I)
  NONE VALUE
    IGNORE
END-DECIDE
#DATEMI  := DIDEMI-DATEMI
PERFORM SISSD002 #DATEMI
MOVE EDITED #DATEMI       (EM=99/99/9999)  TO DATEMI(#I)
*
IF DIDEMI-NUMDOC NE ' '        /* VERONICA - 04.04.17
  MOVE DIDEMI-NUMDOC      TO #NUMDOC(#I)
ELSE
  MOVE EDITED DIDEMI-REGGER (EM=ZZZZZZZZZZZ) TO #NUMDOC(#I)
END-IF
MOVE EDITED DIDEMI-POSTO  (EM=9999)        TO #POSTO(#I)
MOVE EDITED DIDEMI-HOREMI (EM=99:99:99)    TO HOREMI(#I)
MOVE EDITED DIDEMI-MATFUN (EM=9999999)     TO MATRICULA(#I)
MOVE        DIDEMI-UFCONS                  TO #UFCONS(#I)
MOVE        DIDEMI-NOMIDE                  TO #NOMEIDE(#I)
MOVE        DIDEMI-MAEIDE                  TO MAE(#I)
END-SUBROUTINE
********************************
DEFINE SUBROUTINE CHAMA-FONETICA
********************************
RESET #INFORMACOES-GERAIS #CHAVE-COMB-2A21 #CHAVE-COMB-2A22
  #CHAVE-COMB-3A31 #CHAVE-COMB-3A32 #CHAVE-COMB-3A33
  #CHAVE-COMB-4A41 #CHAVE-COMB-4A42 #CHAVE-COMB-4A43
  #CHAVE-COMB-4A44 #CHAVE-COMB-4A45 #CHAVE-COMB-4A46
  #FONETO1(*)
  #K #EXISTE-SEC  #CHAVE-COMBINA
**
#CONSULTA       := TRUE
#TEXTO-CONSULTA := #NOME
CALLNAT 'NATBNX22' #CHAVE-FONETICA
**
RESET #INFORMACOES-GERAIS #CHAVE-COMB-2A21 #CHAVE-COMB-2A22
  #CHAVE-COMB-3A31 #CHAVE-COMB-3A32 #CHAVE-COMB-3A33
  #CHAVE-COMB-4A41 #CHAVE-COMB-4A42 #CHAVE-COMB-4A43
  #CHAVE-COMB-4A44 #CHAVE-COMB-4A45 #CHAVE-COMB-4A46
  #FONETO1(*)
  #K #EXISTE-SEC  #CHAVE-COMBINA
**
#CONSULTA-MAE       := TRUE
#TEXTO-CONSULTA-MAE := #MAE
CALLNAT 'NATBNX22' #CHAVE-FONETICA-MAE
**
END-SUBROUTINE
************************
DEFINE SUBROUTINE ROT-FONETICA
******************************
MOVE +60 TO #TAMANHO-NOME-PESSOA
MOVE 'F' TO #TIPO-NOME
MOVE +0  TO #NUMERO-COMPONENTES
MOVE '8' TO #TIPO-COMBINACAO
MOVE +20 TO #NUMERO-COMBINACOES
MOVE +0  TO #INICIO-COMBINACAO
MOVE '0' TO #FLAG
CALL 'PGP60RFO' #INFORMACOES-GERAIS #CHAVE-COMB-2A21 #CHAVE-COMB-2A22
  #CHAVE-COMB-3A31 #CHAVE-COMB-3A32 #CHAVE-COMB-3A33
  #CHAVE-COMB-4A41 #CHAVE-COMB-4A42 #CHAVE-COMB-4A43
  #CHAVE-COMB-4A44 #CHAVE-COMB-4A45 #CHAVE-COMB-4A46
*
MOVE #NUMERO-COMPONENTES TO #NUMERO2
*
IF #NUMERO2 = 2
  ADD 1 TO #K
  MOVE #CHAVE-NOME-DADO TO #FONETO1 (#K)
END-IF
IF #NUMERO2 = 3
  ADD 1 TO #K
  MOVE #CHAVE-COMB-2A21 TO #CHAVE-COMBINA
  ADD 1 TO #K
  MOVE #VET (1)         TO #FONETO1 (#K)
  ADD 1 TO #K
  MOVE #VET (2)         TO #FONETO1 (#K)
  MOVE #CHAVE-COMB-3A31 TO #CHAVE-COMBINA
  ADD 1 TO #K
  MOVE #VET (1)         TO #FONETO1 (#K)
END-IF
*
IF #NUMERO2 = 4
  MOVE #CHAVE-COMB-2A21 TO #CHAVE-COMBINA
  ADD 1 TO #K
  MOVE #VET (1)         TO #FONETO1 (#K)
  ADD 1 TO #K
  MOVE #VET (2)         TO #FONETO1 (#K)
  ADD 1 TO #K
  MOVE #VET (3)         TO #FONETO1 (#K)
  MOVE #CHAVE-COMB-3A31 TO #CHAVE-COMBINA
  FOR #J2 1 TO 20
    MOVE #VET(#J2) TO #AUX
    IF #AUX = ' '
      ESCAPE BOTTOM
    END-IF
    ADD 1 TO #K
    MOVE #VET(#J2) TO #FONETO1 (#K)
  END-FOR
  MOVE #CHAVE-COMB-4A41 TO #CHAVE-COMBINA
  ADD 1 TO #K
  MOVE #VET(1) TO #FONETO1 (#K)
END-IF
*
IF #NUMERO2 GT 4
  MOVE #CHAVE-COMB-2A21 TO #CHAVE-COMBINA
  FOR #J2 1 TO 20
    MOVE #VET(#J2) TO #AUX
    IF #AUX = ' '
      ESCAPE BOTTOM
    END-IF
    ADD 1 TO #K
    MOVE #VET(#J2) TO #FONETO1 (#K)
  END-FOR
*
  MOVE #CHAVE-COMB-3A31 TO #CHAVE-COMBINA
  FOR #J2 1 TO 20
    MOVE #VET(#J2) TO #AUX
    IF #AUX = ' '
      ESCAPE BOTTOM
    END-IF
    ADD 1 TO #K
    MOVE #VET(#J2) TO #FONETO1 (#K)
  END-FOR
*
  MOVE #CHAVE-COMB-4A41 TO #CHAVE-COMBINA
*   MOVE 0 TO #K
  FOR #J2 1 TO 20
    MOVE #VET(#J2) TO #AUX
    IF #AUX = ' '
      ESCAPE BOTTOM
    END-IF
    ADD 1 TO #K
    MOVE #VET(#J2) TO #FONETO1 (#K)
  END-FOR
  ADD 1 TO #K
  MOVE #CHAVE-NOME-DADO TO #FONETO1 (#K)
END-IF
END-SUBROUTINE
**************************
END
